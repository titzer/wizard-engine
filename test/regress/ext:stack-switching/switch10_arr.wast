;; control flow: main -> $from -> $to -> $from -> main
(module
  (type $s (array (mut i32)))

  (type $f0 (func (result i32 i32)))
  (type $c0 (cont $f0))

  ;; type of $from
  (type $f1 (func (param (ref null $s)) (result i32 i32)))
  (type $c1 (cont $f1))

  ;; type of continuation generated by switching from $to
  (type $f4 (func (param (ref null $c0)) (result i32 i32)))
  (type $c4 (cont $f4))

  ;; type of continuation generated by switching from $from
  (type $f2 (func (param i32 i32 (ref null $c4)) (result i32 i32)))
  (type $c2 (cont $f2))

  ;; type of $to
  (type $f3 (func (param (ref null $s) (ref null $c2)) (result i32 i32)))
  (type $c3 (cont $f3))

  ;; dummy continuation type to test for suspension
  (type $f_dummy (func (param i32 i32) (result i32 i32)))
  (type $c_dummy (cont $f_dummy))

  (tag $e (result i32 i32))

  (func $from (param $i (ref null $s)) (result i32 i32)
    (local.get $i)
    (switch $c3 $e (cont.new $c3 (ref.func $to)))
    (drop)
    (return)
  )
  (elem declare func $from)

  (func $to (param $i (ref null $s)) (param $cont (ref null $c2)) (result i32 i32)
    (i32.add (array.get $s (local.get $i) (i32.const 0)) (i32.const 100))
    (i32.add (array.get $s (local.get $i) (i32.const 1)) (i32.const 200))
    (switch $c2 $e (local.get $cont))
    (unreachable)
  )
  (elem declare func $to)

  (func $wrapper (param $i (ref null $s)) (result i32 i32)
    (block $l (result (ref null $c_dummy))
      (local.get $i)
      (cont.new $c1 (ref.func $from))
      (resume $c1 (on $e $l))
      (return)
    )
    (unreachable)
  )
  (elem declare func $wrapper)

  (func (export "main") (param i32 i32) (result i32 i32)
    (local $v (ref null $s))
    (local.set $v (array.new $s (i32.const 0) (i32.const 2)))
    (array.set $s (local.get $v) (i32.const 0) (local.get 0))
    (array.set $s (local.get $v) (i32.const 1) (local.get 1))
    (local.get $v)
    (resume $c1 (on $e switch) (cont.new $c1 (ref.func $wrapper)))
  )
)

(assert_return (invoke "main" (i32.const 0) (i32.const 0)) (i32.const 100) (i32.const 200))
(assert_return (invoke "main" (i32.const 24) (i32.const 42)) (i32.const 124) (i32.const 242))
(assert_return (invoke "main" (i32.const -100) (i32.const -200)) (i32.const 0) (i32.const 0))
