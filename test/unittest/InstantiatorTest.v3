// Copyright 2020 Ben L. Titzer. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

def T = UnitTests.registerT("inst:", _, InstantiatorTester.new, _);
def X = [
	T("func1", test_func1),
	T("table1", test_table1),
	T("mem1", test_mem1),
	T("mem2", test_mem2),
	T("global1", test_global1),
	T("global2", test_global2),
	T("type1", test_type1),
	T("type2", test_type2),
	T("typesig1", test_typesig1),
	T("typesig2", test_typesig2),
	T("typesig3", test_typesig3),
	T("funcsig1", test_funcsig1),
	T("global3", test_global3),
	T("arg1", test_arg1),
	T("arg2", test_arg2),
	T("arg3", test_arg3),
	()
];

def newSig = SigDecl.new(ValueTypes.NO_HEAPTYPES, _, _);

class InstantiatorTester(t: Tester) extends ModuleBuilder {
	def assert_fail(bindings: Array<Exportable>) {
		var error = ErrorGen.new("InstantiatorTest.v3");
		var instance = Instantiator.new(extensions, module, bindings, error).run();
		if (instance != null) return t.fail("expected instantiation fail, but passed (non-null instance)");
		if (error.ok()) return t.fail("expected instantiation fail, but passed (no error)");
	}
	def assert_pass(bindings: Array<Exportable>) -> Instance {
		var error = ErrorGen.new("InstantiatorTest.v3");
		var instance = Instantiator.new(extensions, module, bindings, error).run();
		if (error.error()) t.fail1("instantiation failed, got %s", error.error_msg);
		if (instance == null) t.fail("instantiation failed, got null");
		return instance;
	}
	def assert_record_args(bindings: Array<Exportable>) -> (Instance, Array<Array<Exportable>>) {
		var error = ErrorGen.new("InstantiatorTest.v3");
		var instantiator = Instantiator.new(extensions, module, Array.new(bindings.length), error);
		var recorder = ImportArgRecorder.new(bindings);
		instantiator.processors.put(recorder);
		var instance = instantiator.run();
		if (error.error()) t.fail1("instantiation failed, got %s", error.error_msg);
		if (instance == null) t.fail("instantiation failed, got null");
		return (instance, recorder.recordedArgs);
	}
	def assert_sig(expected_sig: SigDecl, got_sig: SigDecl) {
		if (got_sig == null) return t.fail1("expected sig %q, got null", expected_sig.render);
		if (!got_sig.isAssignableSig(expected_sig)) {
			t.fail2("expected sig %q, got %q", expected_sig.render, got_sig.render);
		}
	}
	def assert_exportable(expected: Exportable, got: Exportable) {
		if (expected != got) {
			t.fail2("expected exportable %q, got %q", expected.render, got.render);
		}
	}
	def assert_exportables(expected: Array<Exportable>, got: Array<Exportable>) {
		t.assertar("exportable", expected, got, Exportable.render);
	}
}

class ImportArgRecorder(bindings: Array<Exportable>) extends ImportProcessor("test") {
	var recordedArgs = Array<Array<Exportable>>.new(bindings.length);
	var pos: int;

	def processCommand(name: string, args: Array<Exportable>) -> Exportable { return do(args); }
	def processAbsType(name: string, args: Array<Exportable>, decl: AbsTypeDecl) -> Exportable { return do(args); }
	def processFunction(name: string, args: Array<Exportable>, decl: FuncDecl) -> Exportable { return do(args); }
	def processTable(name: string, args: Array<Exportable>, decl: TableDecl) -> Exportable { return do(args); }
	def processMemory(name: string, args: Array<Exportable>, decl: MemoryDecl) -> Exportable { return do(args); }
	def processGlobal(name: string, args: Array<Exportable>, decl: GlobalDecl) -> Exportable { return do(args); }
	def do(args: Array<Exportable>) -> Exportable {
		recordedArgs[pos] = Arrays.dup(args);
		return bindings[pos++];
	}
}

def newAbsTypeDecl = AbsTypeDecl.new(false, false, _);

def newTable(elemtype: ValueType, initial: u32, max: Max) -> Table {
	return Table.new(elemtype, TableDecl.new(elemtype, initial, max));
}

def NONE: Array<Exportable> = [];
def NULL = Array<Exportable>.new(1);
def FUNC: Array<Exportable> = [HostFunction.new(null, SigCache.v_v)];
def TABLE: Array<Exportable> = [newTable(ValueTypes.EXTERNREF, 0, Max.None)];
var MEMORY: Array<Exportable>;
def GLOBAL: Array<Exportable> = [Global.new(ValueType.I32, GlobalDecl.new(ValueType.I32, false, InitExpr.I32(0)))];
def TYPE: Array<Exportable> = [HostType.new()];
def BASIC_TYPES = [
	ValueType.I32, ValueType.I64, ValueType.F32, ValueType.F64, ValueTypes.EXTERNREF, ValueTypes.FUNCREF
];

def memory() -> Array<Exportable> {
	if (MEMORY == null) MEMORY = [Target.newMemory(MemoryDecl.new(0, Max.Set(0), false, ValueType.I32))];
	return MEMORY;
}

def test_func1(t: InstantiatorTester) {
	var sig_decl = SigCache.v_v.dup();
	t.module.addDecl(sig_decl);
	t.module.addImport("I", "f", null, t.newFunc(sig_decl));

	t.assert_fail(NONE);
	t.assert_fail(NULL);
	t.assert_fail(TABLE);
	t.assert_fail(memory());
	t.assert_fail(GLOBAL);
	t.assert_fail(TYPE);
	t.assert_fail([HostFunction.new(null, SigCache.v_i)]);
	t.assert_fail([HostFunction.new(null, SigCache.i_i)]);

	t.assert_pass([HostFunction.new(null, SigCache.v_v)]);
}

def test_table1(t: InstantiatorTester) {
	t.module.addImport("M", "t", null, TableDecl.new(ValueTypes.EXTERNREF, 1, Max.Set(3)));

	t.assert_fail(NONE);
	t.assert_fail(NULL);
	t.assert_fail(FUNC);
	t.assert_fail(memory());
	t.assert_fail(GLOBAL);
	t.assert_fail(TYPE);

	t.assert_fail([newTable(ValueTypes.FUNCREF, 1, Max.Set(3))]);
	t.assert_fail([newTable(ValueType.I32, 1, Max.Set(3))]);
	t.assert_fail([newTable(ValueTypes.EXTERNREF, 0, Max.Set(3))]);
	t.assert_fail([newTable(ValueTypes.EXTERNREF, 1, Max.Set(4))]);
	t.assert_fail([newTable(ValueTypes.EXTERNREF, 1, Max.None)]);

	t.assert_pass([newTable(ValueTypes.EXTERNREF, 1, Max.Set(3))]);
	t.assert_pass([newTable(ValueTypes.EXTERNREF, 2, Max.Set(3))]);
	t.assert_pass([newTable(ValueTypes.EXTERNREF, 1, Max.Set(2))]);
}

def test_mem1(t: InstantiatorTester) {
	t.module.addImport("N", "m", null, MemoryDecl.new(1, Max.Set(3), false, ValueType.I32));

	t.assert_fail(NONE);
	t.assert_fail(NULL);
	t.assert_fail(FUNC);
	t.assert_fail(TABLE);
	t.assert_fail(GLOBAL);
	t.assert_fail(TYPE);

	t.assert_fail([Target.newMemory(MemoryDecl.new(0, Max.Set(3), false, ValueType.I32))]);
	t.assert_fail([Target.newMemory(MemoryDecl.new(1, Max.Set(4), false, ValueType.I32))]);
	t.assert_fail([Target.newMemory(MemoryDecl.new(1, Max.None, false, ValueType.I32))]);
	t.assert_fail([Target.newMemory(MemoryDecl.new(1, Max.Set(3), true, ValueType.I32))]);

	t.assert_pass([Target.newMemory(MemoryDecl.new(1, Max.Set(3), false, ValueType.I32))]);
	t.assert_pass([Target.newMemory(MemoryDecl.new(1, Max.Set(2), false, ValueType.I32))]);
}

def test_mem2(t: InstantiatorTester) {
	t.module.addImport("N", "m", null, MemoryDecl.new(1, Max.Set(3), true, ValueType.I32));

	t.assert_fail([Target.newMemory(MemoryDecl.new(1, Max.Set(3), false, ValueType.I32))]);

	t.assert_pass([Target.newMemory(MemoryDecl.new(1, Max.Set(3), true, ValueType.I32))]);
	t.assert_pass([Target.newMemory(MemoryDecl.new(1, Max.Set(2), true, ValueType.I32))]);
}

def test_global1(t: InstantiatorTester) {
	t.module.addImport("H", "g", null, GlobalDecl.new(ValueType.I64, false, InitExpr.I64(5)));

	t.assert_fail(NONE);
	t.assert_fail(NULL);
	t.assert_fail(FUNC);
	t.assert_fail(TABLE);
	t.assert_fail(memory());
	t.assert_fail(TYPE);

	t.assert_fail([Global.new(ValueType.I32, GlobalDecl.new(ValueType.I32, false, InitExpr.I32(55)))]);
	t.assert_fail([Global.new(ValueType.I64, GlobalDecl.new(ValueType.I64, true, InitExpr.I64(55)))]);

	t.assert_pass([Global.new(ValueType.I64, GlobalDecl.new(ValueType.I64, false, InitExpr.I64(55)))]);
}

def test_global2(t: InstantiatorTester) {
	t.module.addImport("H", "g", null, GlobalDecl.new(ValueType.F64, true, InitExpr.F64(5)));

	t.assert_fail([Global.new(ValueType.I32, GlobalDecl.new(ValueType.I32, false, InitExpr.I32(55)))]);
	t.assert_fail([Global.new(ValueType.F64, GlobalDecl.new(ValueType.F64, false, InitExpr.I64(55)))]);

	t.assert_pass([Global.new(ValueType.F64, GlobalDecl.new(ValueType.F64, true, InitExpr.I64(55)))]);
}

def test_type1(t: InstantiatorTester) {
	t.module.addImport("Y", "p", null, newAbsTypeDecl(ValueTypes.NONE));

	t.assert_fail(NONE);
	t.assert_fail(NULL);
	t.assert_fail(FUNC);
	t.assert_fail(TABLE);
	t.assert_fail(memory());
	t.assert_fail(GLOBAL);

	t.assert_pass([HostType.new()]);
}

def test_type2(t: InstantiatorTester) {
	var args = Array<Exportable>.new(1);

	for (x in BASIC_TYPES) {
		for (y in BASIC_TYPES) {
			var n = InstantiatorTester.new(t.t);
			n.module.addImport("Q", "r", null, newAbsTypeDecl([x]));
			args[0] = ExportedValueType.new(null, y);
			if (ValueTypes.isAssignable(y, x)) n.assert_pass(args);
			else n.assert_fail(args);
		}
	}

}

def test_typesig1(t: InstantiatorTester) {
	var at = newAbsTypeDecl(ValueTypes.NONE), ata: Array<ValueType> = [ValueType.Abstract(at)];
	t.module.addImport("Y", "p", null, at);
	var sig_decl = newSig(ata, ata);
	t.module.addDecl(sig_decl);

	var ht = HostType.new();
	var i = t.assert_pass([ht]);
	if (i == null) return;
	var expected_sig = newSig([ValueType.Host(ht)], [ValueType.Host(ht)]);
	var got_sig = SigDecl.!(i.heaptypes[sig_decl.heaptype_index]);
	t.assert_sig(expected_sig, got_sig);
}

def test_typesig2(t: InstantiatorTester) {
	for (x in BASIC_TYPES) {
		for (y in BASIC_TYPES) {
			var n = InstantiatorTester.new(t.t);

			var xt = newAbsTypeDecl(ValueTypes.NONE);
			var yt = newAbsTypeDecl(ValueTypes.NONE);
			n.module.addImport("Z", "x", null, xt);
			n.module.addImport("Z", "y", null, yt);
			var sig_decl = newSig([ValueType.Abstract(xt)], [ValueType.Abstract(yt)]);
			n.module.addDecl(sig_decl);


			var i = n.assert_pass([ExportedValueType.new(null, x), ExportedValueType.new(null, y)]);
			if (i == null) return;
			var expected_sig = newSig([x], [y]);
			var got_sig = SigDecl.!(i.heaptypes[sig_decl.heaptype_index]);
			n.assert_sig(expected_sig, got_sig);
		}
	}
}

def test_typesig3(t: InstantiatorTester) {
	for (i < 3) {
		for (j < 3) {
			var n = InstantiatorTester.new(t.t);

			var x = ValueTypes.FUNCREF;
			var xt = newAbsTypeDecl(ValueTypes.NONE);
			n.module.addImport("Z", "x", null, xt);
			var pt = [ValueType.I32, ValueType.I64, ValueType.F32], rt = Arrays.dup(pt);
			pt[i] = ValueType.Abstract(xt);
			rt[j] = ValueType.Abstract(xt);
			var sig_decl = newSig(Arrays.dup(pt), Arrays.dup(rt));
			n.module.addDecl(sig_decl);


			var instance = n.assert_pass([ExportedValueType.new(null, x)]);
			if (instance == null) return;
			pt[i] = x;
			rt[j] = x;
			var expected_sig = newSig(Arrays.dup(pt), Arrays.dup(rt));
			var got_sig = SigDecl.!(instance.heaptypes[sig_decl.heaptype_index]);
			n.assert_sig(expected_sig, got_sig);
		}
	}
}

def test_funcsig1(t: InstantiatorTester) {
	var at = newAbsTypeDecl(ValueTypes.NONE), ata: Array<ValueType> = [ValueType.Abstract(at)];
	t.module.addImport("Y", "p", null, at);
	var sig_decl = t.addSig(newSig(ata, ata));
	var func_decl = t.newFunc(sig_decl);

	var ht = HostType.new();
	var i = t.assert_pass([ht]);
	if (i == null) return;
	var expected_sig = newSig([ValueType.Host(ht)], [ValueType.Host(ht)]);
	var got_sig = SigDecl.!(i.heaptypes[sig_decl.heaptype_index]);
	t.assert_sig(expected_sig, got_sig);
	var got_func = i.functions[func_decl.func_index];
	t.assert_sig(expected_sig, got_func.sig);
}

def test_global3(t: InstantiatorTester) {
	var at = newAbsTypeDecl(ValueTypes.NONE);
	t.module.addImport("Y", "p", null, at);
	var global_decl = GlobalDecl.new(ValueType.Abstract(at), false, InitExpr.I32(0));
	t.module.addDecl(global_decl);

	var ht = HostType.new();
	var i = t.assert_pass([ht]);
	if (i == null) return;
	var got_global = i.globals[global_decl.global_index];
	var expected_type = ValueType.Host(ht);
	if (got_global.valtype != expected_type) {
		t.t.fail2("expected global type %q, got %q", expected_type.render, got_global.valtype.render);
	}
}

def test_arg1(t: InstantiatorTester) {
	var sig_decl = SigCache.v_v.dup();
	t.module.addDecl(sig_decl);
	var func_decl = t.newFunc(sig_decl);
	t.module.addDecl(func_decl);
	t.module.addImport("I", "f", [func_decl], t.newFunc(sig_decl));

	t.assert_fail(NONE);
	t.assert_fail(NULL);
	t.assert_fail(TABLE);
	t.assert_fail(memory());
	t.assert_fail(GLOBAL);
	t.assert_fail(TYPE);
	t.assert_fail([HostFunction.new(null, SigCache.v_i)]);
	t.assert_fail([HostFunction.new(null, SigCache.i_i)]);

	t.assert_pass([HostFunction.new(null, SigCache.v_v)]);
}

def test_arg2(t: InstantiatorTester) {
	var sig_decl = SigCache.v_v.dup();
	t.module.addDecl(sig_decl);
	var func_decl = t.newFunc(sig_decl);
	t.module.addDecl(func_decl);
	t.module.addImport("test", "f", [func_decl], t.newFunc(sig_decl));

	var g = t.assert_record_args([HostFunction.new(null, SigCache.v_v)]), i = g.0, r = g.1;
	if (i == null) return;

	t.assert_exportable(i.functions[func_decl.func_index], r[0][0]);
}

def test_arg3(t: InstantiatorTester) {
	var at = newAbsTypeDecl(ValueTypes.NONE), vt = ValueType.Abstract(at);
	t.module.addImport("test", "xt", null, at);
	var sig_decl = newSig([vt], SigCache.arr_v);
	t.module.addDecl(sig_decl);
	var func_decl = t.newFunc(sig_decl);
	t.module.addDecl(func_decl);
	t.module.addImport("test", "f", [at, func_decl], t.newFunc(sig_decl));

	var ht = HostType.new();
	var hsig = newSig([ValueType.Host(ht)], SigCache.arr_v);
	var hf = HostFunction.new(null, hsig);

	var g = t.assert_record_args([ht, hf]), i = g.0, r = g.1;
	if (i == null) return;

	t.assert_exportables([], r[0]);
	t.assert_exportable(ht, i.abstypes[at.abstype_index]);

	t.assert_exportables([ht, i.functions[func_decl.func_index]], r[1]);
	t.assert_sig(hsig, i.functions[func_decl.func_index].sig);
}

// TODO: more tests for imported function sig compatibility
// TODO: more tests for table elemtype compatibility
// TODO: more tests for global valtype compatibility
