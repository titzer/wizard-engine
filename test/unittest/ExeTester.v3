// Copyright 2024 Wizard authors. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

// Registration for test tiers on various platforms.
component TestTiers {
	def addTests(array: Array<(string, ExeTester -> void)>) {
		for (t in array) {
			for (l = Target.getTestTiers(); l != null; l = l.tail) {
				var name = l.head.0, tiering = l.head.1;
				UnitTests.registerT(name, t.0, ExeTester.new(_, tiering), t.1);
			}
		}

	}
	def addTests2<T>(array: Array<(string, (Tester, ExecutionStrategy) -> T, T -> void)>) {
		for (t in array) {
			for (l = Target.getTestTiers(); l != null; l = l.tail) {
				var name = l.head.0, tiering = l.head.1;
				UnitTests.registerT(name, t.0, t.1(_, tiering), t.2);
			}
		}
	}
}

def NO_IMPORTS = Array<Exportable>.new(0);
def NONE = Result.Value(Values.NONE);
class ExeTester(t: Tester, tiering: ExecutionStrategy) extends ModuleBuilder {
	var max_call_depth = 100u;
	var imports = NO_IMPORTS;
	var args: Array<Value>;

	new() {
		Execute.reset();
		Execute.tiering = tiering; // XXX: global state not ideal here.
	}

	def codev(raw: Array<byte>) -> this {
		code(raw);
		var err = ErrorGen.new(module.filename);
		var v = CodeValidator.new(extensions, Limits.new().set(extensions), module, err);
		tiering.onFuncValidationStart(module, func);
		var r = v.validateFunc(func);
		tiering.onFuncValidationFinish(module, func, err);
		match (r) {
			Ok => ;
			Error(error, pc, msg) => t.fail2("invalid code @+%d: %s", pc, msg);
		}
	}
	def makeInstance() -> Instance {
		var err = ErrorGen.new("ExeTester.v3");
		var instance = Instantiator.new(extensions, module, imports, err).run();
		if (!err.ok()) {
			t.fail(err.error_msg);
			return null;
		}
		tiering.onTestModule(module);
		var f = instance.functions[0];
		if (WasmFunction.?(f)) tiering.onTestRun(WasmFunction.!(f), err);
		if (!err.ok()) {
			t.fail(err.error_msg);
			return null;
		}
		return instance;
	}
	def makeFunc() -> Function {
		var instance = this.makeInstance();
		return if(instance != null, instance.functions[0]);
	}
	def run(args: Array<Value>) -> (Instance, Result) {
		var instance = makeInstance();
		if (instance == null) return (instance, Traps.result(TrapReason.ERROR));
		var f = instance.functions[0];
		Execute.limits.max_call_depth = max_call_depth;
		if (Trace.interpreter) {
			Trace.OUT.puts("run ");
			Values.renderVals(Trace.OUT, args);
			Trace.OUT.outln();
		}
		var r = Execute.call(f, args);

		if (Trace.interpreter) {
			Trace.OUT.put1(" -> %q\n", r.render).outln();
		}
		return (instance, r);
	}
	// Methods to set up arguments.
	def noargs() -> this					{ args = Values.NONE; }
	def argsN(x: Array<Value>) -> this			{ args = x; }
	def args_i(x: i32) -> this				{ args = [Value.I32(u32.view(x))]; }
	def args_u(x: u32) -> this				{ args = [Value.I32(x)]; }
	def args_l(x: i64) -> this				{ args = [Value.I64(u64.view(x))]; }
	def args_w(x: u64) -> this				{ args = [Value.I64(x)]; }
	def args_f(x: u32) -> this				{ args = [Value.F32(x)]; }
	def args_d(x: u64) -> this				{ args = [Value.F64(x)]; }
	def args_s(x: u64, y: u64) -> this			{ args = [Value.V128(x, y)]; }
	def args_r(x: Object) -> this				{ args = [Value.Ref(x)]; }
	def args_ii(x: i32, y: i32) -> this			{ args = [Value.I32(u32.view(x)), Value.I32(u32.view(y))]; }
	def args_uu(x: u32, y: u32) -> this			{ args = [Value.I32(x), Value.I32(y)]; }
	def args_uuu(x: u32, y: u32, z: u32) -> this		{ args = [Value.I32(x), Value.I32(y), Value.I32(z)]; }
	def args_ll(x: i64, y: i64) -> this			{ args = [Value.I64(u64.view(x)), Value.I64(u64.view(y))]; }
	def args_ww(x: u64, y: u64) -> this			{ args = [Value.I64(x), Value.I64(y)]; }
	def args_ff(x: u32, y: u32) -> this			{ args = [Value.F32(x), Value.F32(y)]; }
	def args_dd(x: u64, y: u64) -> this			{ args = [Value.F64(x), Value.F64(y)]; }
	def args_ss(x: (u64, u64), y: (u64, u64)) -> this	{ args = [Value.V128(x), Value.V128(y)]; }

	// Methods to run (with previously-set arguments) and assert the result.
	def assert2_i(x: i32) -> this			{ assert_r(args, Result.Value([Value.I32(u32.view(x))])); }
	def assert2_u(x: u32) -> this			{ assert_r(args, Result.Value([Value.I32(x)])); }
	def assert2_l(x: i64) -> this			{ assert_r(args, Result.Value([Value.I64(u64.view(x))])); }
	def assert2_w(x: u64) -> this			{ assert_r(args, Result.Value([Value.I64(x)])); }
	def assert2_f(x: u32) -> this			{ assert_r(args, Result.Value([Value.F32(x)])); }
	def assert2_d(x: u64) -> this			{ assert_r(args, Result.Value([Value.F64(x)])); }
	def assert2_s(x: u64, y: u64) -> this		{ assert_r(args, Result.Value([Value.V128(x, y)])); }
	def assert2_r(x: Object) -> this		{ assert_r(args, Result.Value([Value.Ref(x)])); }
	def assert2_res(x: Result) -> this		{ assert_r(args, x); }
	def assert2_val(x: Value) -> this		{ assert_r(args, Result.Value([x])); }
	def assert2_none() -> this			{ assert_r(args, NONE); }
	def assert2_nan() -> this			{ assert_nan(args); }
	def assert2_func(findex: int) -> this		{ assert_g(args, findex); }
	def assert2_trap(reason: TrapReason) -> this	{ assert_r(args, Traps.result(reason)); }
	def assert2_trap_at(reason: TrapReason, pcs: Array<int>) -> this	{ assert_trap_at(args, reason, pcs); }
	def assert2_mem(offset: u32, expected: Array<byte>) -> this { assert_mem(args, offset, expected); }
	def assert2_ftable(offset: u32, expected: Array<FuncDecl>) -> this { assert_ftable(args, offset, expected); }

	def assert_req(got: Result, expected: Result) {
		if (!expected.equals(got)) t.fail2("expected %q, got %q", expected.render, got.render);
	}
	private def assert_mem(args: Array<Value>, offset: u32, expected: Array<byte>) {
		var tr = run(args), instance = tr.0, got = tr.1;
		assert_req(NONE, got);
		if (instance == null) return t.fail("instance is null");
		var mem = instance.memories[0];
		if (mem == null) return t.fail("memory is null");
		for (i < expected.length) {
			var addr = int.!(offset) + i;
			var got = mem.read_u8(0, u32.view(addr)).result;
			if (expected[i] != got) return t.fail3("expected mem[%x] = %x, got %x", addr, expected[i], got);
		}
	}
	private def assert_ftable(args: Array<Value>, offset: u32, expected: Array<FuncDecl>) {
		var tr = run(args), instance = tr.0, got = tr.1;
		assert_req(NONE, got);
		var elems = instance.tables[0].elems;
		if (elems.length < expected.length) return t.fail2("expected table.length >= %d, got %d", expected.length, elems.length);
		for (i < expected.length) {
			var addr = int.!(offset) + i;
			var exp = expected[i];
			var got = Value.Ref.!(elems[addr]).val;
			if (exp == null) {
				if (got != null) return t.fail1("expected table[%d] == null", addr);
			} else {
				if (!WasmFunction.?(got)) return t.fail2("expected table[%d] : WasmFunction%s ", addr, if(got == null, ", got null", ""));
				var wgot = WasmFunction.!(got);
				if (exp != wgot.decl) return t.fail3("expected table[%d] = #%d, got #%d", addr, expected[i].func_index, wgot.decl.func_index);
			}
		}
	}
	private def assert_r(args: Array<Value>, expected: Result) -> Result {
		var got = run(args).1;
		if (!expected.equals(got)) t.fail2("expected %q, got %q", expected.render, got.render);
		return got;
	}
	private def assert_nan(args: Array<Value>) {
		var got = run(args).1;
		match (got) {
			Value(vals) => match (vals[0]) {
				F32(bits) => if (Floats.isNan32(bits)) return;
				F64(bits) => if (Floats.isNan64(bits)) return;
				_ => ;
			}
			_ => ;
		}
		t.fail1("expected NaN, got %q", got.render);
	}
	private def assert_g(args: Array<Value>, findex: int) {
		var r = run(args), instance = r.0, got = r.1;
		var expected = Result.Value([Value.Ref(instance.functions[findex])]);
		if (expected.equals(got)) return;
		t.fail2("expected %q, got %q", expected.render, got.render);
	}
	private def assert_trap_at(args: Array<Value>, expected: TrapReason, pcs: Array<int>) {
		var got = assert_r(args, Traps.result(expected));
		match (got) {
			Throw(thrown) => match (thrown) {
				x: Trap => {
					if (x.stacktrace == null) return t.fail("expected stacktrace, but none");
					var f = x.stacktrace.frames;
					if (f == null) return t.fail("expected stacktrace frames, got null");
					if (f.length != pcs.length) return t.fail2("expected %d stacktrace frame, got %d", pcs.length, f.length);
					for (i < pcs.length) {
						var pc = pcs[i], gotpc = f[i].1;
						if (gotpc != pc) return t.fail3("expected @ frame %d, pc=%d, got pc=%d", i, pc, gotpc);
					}
				}
				_ => t.fail("expected trap, got other error");
			}
			_ => t.fail("expected trap");
		}
	}
	private def assert_val1<V>(got: Result, kind: string) -> (bool, V) {
		match (got) {
			Value(vals) => if (vals.length == 1) {
				var v = vals[0];
				if (V.?(v)) return (true, V.!(v));
			}
			_ => ;
		}
		t.fail2("expected 1 %s value, got %q", kind, got.render);
		var d: V;
		return (false, d);
	}
	def assert_struct(got: Result, expected_struct: StructDecl, rtt: HeapTypeDecl, fields: Array<Value>) {
		var r = assert_val1<Value.Ref>(got, "struct"), ok = r.0, hr = r.1;
		if (!ok) return;
		if (!HeapStruct.?(hr.val)) return t.fail1("expected struct, got %q", got.render);
		var obj = HeapStruct.!(hr.val);
		if (obj.decl != expected_struct) return t.fail2("expected heap %q, got %q",
			expected_struct.render, obj.decl.render);
		if (rtt != null && obj.decl != rtt) return t.fail2("expected rtt %q, got %q", rtt.render, obj.decl.render);
		t.assertar("fields", fields, obj.vals, Values.render);
	}
	def assert_array(got: Result, expected_array: ArrayDecl, rtt: HeapTypeDecl, elems: Array<Value>) {
		var r = assert_val1<Value.Ref>(got, "array"), ok = r.0, hr = r.1;
		if (!ok) return;
		if (!HeapArray.?(hr.val)) return t.fail1("expected array, got %q", got.render);
		var obj = HeapArray.!(hr.val);
		if (obj.decl != expected_array) return t.fail2("expected heap %q, got %q",
			expected_array.render, obj.decl.render);
		if (rtt != null && obj.decl != rtt) return t.fail2("expected rtt %q, got %q", rtt.render, obj.decl.render);
		t.assertar("elements", elems, obj.vals, Values.render);
	}
}

