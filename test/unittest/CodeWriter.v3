// Copyright 2025 Wizard authors. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

class CodeWriter extends DataWriter {
	new() super () { }
	def putOp(op: Opcode) -> this {
		if (op.prefix == 0) {
			putb(op.code & 0xFF);
		} else {
			putb(op.prefix);
			var page = Opcodes.page_by_prefix[op.prefix];
			if (page.oneByte) {
				putb(op.code & 0xFF);
			} else {
				put_sleb32(op.code);
			}
		}
	}
	def putValueType(vt: ValueType) -> this {
		var tc: i32 = 0;
		match (vt) {
			I32 => tc = BpTypeCode.I32.val;
			I64 => tc = BpTypeCode.I64.val;
			F32 => tc = BpTypeCode.F32.val;
			F64 => tc = BpTypeCode.F64.val;
			V128 => tc = BpTypeCode.V128.val;
			Ref(nullable, ht) => {
				match (ht) {
					ANY       => if (nullable) tc = BpTypeCode.ANYREF.val;
					EXTERN    => if (nullable) tc = BpTypeCode.EXTERNREF.val;
					EQ        => if (nullable) tc = BpTypeCode.EQREF.val;
					I31       => if (nullable) tc = BpTypeCode.I31REF.val;
					EXN       => if (nullable) tc = BpTypeCode.EXNREF.val;
					NONE      => if (nullable) tc = BpTypeCode.NULLREF.val;
					NOFUNC    => if (nullable) tc = BpTypeCode.NULLFUNCREF.val;
					NOEXTERN  => if (nullable) tc = BpTypeCode.NULLEXTERNREF.val;
					NOCONT    => if (nullable) tc = BpTypeCode.NULLCONTREF.val;
					Func(x)   => if (nullable && x == null)
							tc = BpTypeCode.FUNCREF.val;
					Struct(x) => if (nullable && x == null)
							tc = BpTypeCode.STRUCTREF.val;
					Array(x)  => if (nullable && x == null)
							tc = BpTypeCode.ARRAYREF.val;
					_ => ;
				}
				if (tc == 0)
					tc = if(nullable, BpTypeCode.REF_NULL.val, BpTypeCode.REF.val);
			}
			_ => ;
		}
		put_sleb32(tc);
		match (tc) {
			BpTypeCode.REF.val, BpTypeCode.REF_NULL.val =>
				putHeapType(ValueType.Ref.!(vt).heap);
			_ => ;
		}
	}
	def putHeapType(ht: HeapType) -> this {
		match (ht) {
			Func(x)   => put_sleb32(if(x == null, BpHeapTypeCode.FUNC.val, x.heaptype_index));
			EXTERN    => put_sleb32(BpHeapTypeCode.EXTERN.val);
			ANY       => put_sleb32(BpHeapTypeCode.ANY.val);
			EQ        => put_sleb32(BpHeapTypeCode.EQ.val);
			I31       => put_sleb32(BpHeapTypeCode.I31.val);
			NOFUNC    => put_sleb32(BpHeapTypeCode.NOFUNC.val);
			NOEXTERN  => put_sleb32(BpHeapTypeCode.NOEXTERN.val);
			NOCONT    => put_sleb32(BpHeapTypeCode.NOCONT.val);
			Struct(x) => put_sleb32(if(x == null, BpHeapTypeCode.STRUCT.val, x.heaptype_index));
			Array(x)  => put_sleb32(if(x == null, BpHeapTypeCode.ARRAY.val, x.heaptype_index));
			EXN       => put_sleb32(BpHeapTypeCode.EXN.val);
			NONE      => put_sleb32(BpHeapTypeCode.NONE.val);

			_ => ;
		}
	}
}
