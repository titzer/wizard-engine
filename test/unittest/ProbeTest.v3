// Copyright 2022 Ben L. Titzer. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

def T = UnitTests.registerT("probe:", _, ProbeTester.new, _);
def X = [
	T("list1", test_list1),
	T("list2", test_list2),
	T("list3", test_list3),
	T("count0g", test_count_0g),
	T("count_loop", test_count_loop),
	T("loop_rm", test_loop_rm),
	T("enable0i", test_enable_0i),
	T("enable1g", test_enable_1g),
	T("remove0i", test_remove_0i),
	T("remove1i", test_remove_1i),
	T("remove2i", test_remove_2i),
	()
];

class ProbeTester extends DebugTester {
	new(t: Tester) super(t) { }

	def codev(raw: Array<byte>) -> this {
		code(raw);
		var v = CodeValidator.new(extensions, Limits.new().set(extensions), module, ErrorGen.new(module.filename));
		var r = v.validateFunc(func);
		match (r) {
			Ok => ;
			Error(error, pc, msg) => t.fail2("invalid code @+%d: %s", pc, msg);
		}
	}
	def stdCode() {
		sig(SigCache.v_i);
		code([
			Opcode.I32_CONST.code, 22,
			Opcode.I32_CONST.code, 33,
			Opcode.I32_SUB.code
		]);
	}
	def loopCode() {
		sig(SigCache.i_i);
		codev([Opcode.LOOP.code, BpTypeCode.EmptyBlock.code,
			Opcode.LOCAL_GET.code, 0,
			Opcode.I32_CONST.code, 1,
			Opcode.I32_SUB.code,
			Opcode.LOCAL_TEE.code, 0,
			Opcode.BR_IF.code, 0,
			Opcode.END.code,
			Opcode.LOCAL_GET.code, 0]);
	}
	def assert_count(expected: int, p: CountProbe) {
		if (p.count != expected) return t.fail2("expected count = %d, got %d", expected, p.count);
	}
	def assert_countr(min: int, max: int, p: CountProbe) {
		if (p.count < min || p.count > max) return t.fail3("expected count in [%d, %d], got %d", min, max, p.count);
	}
	def assert_list(expected: Array<Probe>, l: ProbeList) {
		var h = l.elem;
		for (p in expected) {
			if (h == null) return t.fail("probe list too short");
			if (h.probe != p) return t.fail("probe mismatch");
			h = h.next;
		}
		if (h != null) return t.fail("probe list too long");
	}
}

def test_list1(t: ProbeTester) {
	var l = ProbeList.new();
	var p1 = CountProbe.new(), p2 = CountProbe.new(), p3 = ClosureProbe.new(l.remove, (p2));

	t.assert_list([], l);
	l.add(p1);
	t.assert_list([p1], l);
	l.add(p2);
	t.assert_list([p1, p2], l);
	l.add(p2);
	t.assert_list([p1, p2, p2], l);
	l.add(p3);
	t.assert_list([p1, p2, p2, p3], l);

	l.fire(null, 0);
	t.assert_count(1, p1);
	t.assert_count(2, p2);
	t.assert_list([p1, p2, p3], l);

	l.fire(null, 0);
	t.assert_count(2, p1);
	t.assert_count(3, p2);
	t.assert_list([p1, p3], l);
}

def test_list2(t: ProbeTester) {
	var l = ProbeList.new();
	var p1 = CountProbe.new(), p2 = CountProbe.new(), p3 = ClosureProbe.new(l.remove, (p2));

	l.add(p3);
	l.add(p2);
	l.add(p2);
	t.assert_list([p3, p2, p2], l);

	l.fire(null, 0);
	t.assert_count(2, p2);
	t.assert_list([p3, p2], l);

	l.fire(null, 0);
	t.assert_count(3, p2);
	t.assert_list([p3], l);
}

class UidProbe(counter: Array<int>) extends Probe {
	var num = -1;
	def fire(func: WasmFunction, offset: int) -> Resumption {
		num = counter[0]++;
		return Resumption.Continue;
	}
}

def test_list3(t: ProbeTester) {
	var l = ProbeList.new();
	var counter = [1001];
	var p1 = UidProbe.new(counter), p2 = UidProbe.new(counter), p3 = UidProbe.new(counter);

	l.add(p1);
	l.add(p2);
	l.add(p3);
	t.assert_list([p1, p2, p3], l);

	l.fire(null, 0);
	t.t.asserteq(1001, p1.num);
	t.t.asserteq(1002, p2.num);
	t.t.asserteq(1003, p3.num);

	l.remove(p2);
	t.assert_list([p1, p3], l);

	l.fire(null, 0);
	t.t.asserteq(1004, p1.num);
	t.t.asserteq(1002, p2.num);
	t.t.asserteq(1005, p3.num);

	l.add(p2);
	t.assert_list([p1, p3, p2], l);

	l.fire(null, 0);
	t.t.asserteq(1006, p1.num);
	t.t.asserteq(1008, p2.num);
	t.t.asserteq(1007, p3.num);
}

def test_count_0g(t: ProbeTester) {
	t.stdCode();
	var p = CountProbe.new();
	var exe = t.exe();
	exe.probes.add(p);
	var got = exe.call(t.f, []);
	var expected = Result.Value([Values.i_v(-11)]);
	if (!expected.equals(got)) return t.t.fail2("expected result = %q, got %q", expected.render, got.render);
	t.assert_countr(4, 6, p);
}

def test_count_loop(t: ProbeTester) {
	t.loopCode();
	var p = CountProbe.new();
	var exe = t.exe();
	t.module.insertProbeAt(t.func.func_index, 3, p);
	exe.call(t.f, [Values.i_v(4)]);
	t.assert_count(4, p);
}

def test_loop_rm(t: ProbeTester) {
	t.loopCode();
	var p1 = CountProbe.new(), p2 = CountProbe.new();
	var exe = t.exe();
	t.module.insertProbeAt(t.func.func_index, 3, p1);
	t.module.insertProbeAt(t.func.func_index, 3, ClosureProbe.new(t.module.removeProbeAt, (t.func.func_index, 3, p1)));
	t.module.insertProbeAt(t.func.func_index, 5, p2);
	exe.call(t.f, [Values.i_v(4)]);
	t.assert_count(1, p1);
	t.assert_count(4, p2);
}

def test_enable_0i(t: ProbeTester) {
	t.stdCode();
	var p = CountProbe.new();
	t.module.insertProbeAt(t.func.func_index, 1, ClosureProbe.new(t.module.insertProbeAt, (t.func.func_index, 3, p)));
	var exe = t.exe();
	exe.call(t.f, []);
	t.assert_count(1, p);
}

def test_enable_1g(t: ProbeTester) {
	t.stdCode();
	var p = CountProbe.new();
	var exe = t.exe();
	exe.probes.add(ClosureProbe.new(t.module.insertProbeAt, (t.func.func_index, 3, p)));
	exe.call(t.f, []);
	if (p.count < 1) t.t.fail("expected probe to fire");
}

def test_remove_0i(t: ProbeTester) {
	t.stdCode();
	var p = CountProbe.new();
	t.module.insertProbeAt(t.func.func_index, 1, p);
	var exe = t.exe();

	exe.call(t.f, []);
	t.assert_count(1, p);

	t.module.removeProbeAt(t.func.func_index, 1, p);
	exe.call(t.f, []);
	t.assert_count(1, p);
}

def test_remove_1i(t: ProbeTester) {
	t.stdCode();
	var p = CountProbe.new();
	t.module.insertProbeAt(t.func.func_index, 1, ClosureProbe.new(t.module.removeProbeAt, (t.func.func_index, 1, p)));
	t.module.insertProbeAt(t.func.func_index, 1, p);
	var exe = t.exe();

	exe.call(t.f, []);
	t.assert_count(1, p);

	exe.call(t.f, []);
	t.assert_count(1, p);
}

def test_remove_2i(t: ProbeTester) {
	t.stdCode();
	var p = CountProbe.new();
	t.module.insertProbeAt(t.func.func_index, 1, p);
	t.module.insertProbeAt(t.func.func_index, 1, ClosureProbe.new(t.module.removeProbeAt, (t.func.func_index, 1, p)));
	var exe = t.exe();

	exe.call(t.f, []);
	t.assert_count(1, p);

	exe.call(t.f, []);
	t.assert_count(1, p);
}