// Copyright 2022 Ben L. Titzer. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

// TODO: make SPC tests capable of running in v3i
def T = UnitTests.registerT("spc:", _, SpcTester.new, _);
def X = [
	T("pool0", test_pool0),
	T("pool1", test_pool1),
	T("pool2", test_pool2),
	T("pool3", test_pool3),
	T("findSpill", test_findSpill),
	T("alias0", test_alias0),
	T("frequency0", test_frequency0),
	T("pool_alloc0", test_pool_alloc0),
	T("pool_alloc1", test_pool_alloc1),
	T("pool_alloc2", test_pool_alloc2),
	T("pool_free0", test_pool_free0),
	T("pool_free1", test_pool_free1),
	T("pool_freeAll", test_pool_freeAll),
	T("pool_unfree0", test_pool_unfree0),
	()
];

def NO_REG = Reg(0);
def RA = Reg(1);
def RB = Reg(2);
def RC = Reg(3);
def XA = Reg(4);
def XB = Reg(5);
def POOL_0 = RegPool32.new([RA, RB, RC]);
def POOL_1 = RegPool32.new([XA, XB]);
def POOLS = [POOL_0.regs, POOL_1.regs];

def regConfig = setupRegConfig();
def poolMap = regConfig.poolMap;
def regSet = regConfig.regSet;

def setupRegConfig() -> RegConfig {
	var regSet = RegSet.new([NO_REG, RA, RB, RC, XA, XB], [null, "%ra", "%rb", "%rc", "%xa", "%xb"]);
	var regConfig = RegConfig.new(regSet);
	var poolMap = PoolMap.new(regSet, 2);

	for (r in POOL_0.regs) poolMap.regToPool[r.index] = 0;
	for (r in POOL_1.regs) poolMap.regToPool[r.index] = 1;

	poolMap.kindToPool[ValueKind.REF.tag] = 0;
	poolMap.kindToPool[ValueKind.I32.tag] = 0;
	poolMap.kindToPool[ValueKind.I64.tag] = 0;
	poolMap.kindToPool[ValueKind.F32.tag] = 1;
	poolMap.kindToPool[ValueKind.F64.tag] = 1;
	poolMap.kindToPool[ValueKind.V128.tag] = 1;

	regConfig.poolMap = poolMap;
	regConfig.regPools = [POOL_0, POOL_1];
	return regConfig;
}

class SpcTester(t: Tester) {
	var regAlloc = RegAlloc.new(regConfig.poolMap, regConfig.regPools, null);

	def alloc() -> Reg {
		return regAlloc.alloc(ValueKind.I32, 22);
	}
	def assertFree(expected: bool, r: Reg) {
		var got = regAlloc.isFree(r);
		if (got != expected) t.fail3("expected isFree(%s) == %z, got %z", regSet.getName(r), expected, got);
	}
	def assertInPool(pool: int, r: Reg) {
		for (rj in POOLS[pool]) {
			if (r == rj) return;
		}
		t.fail2("expected register in pool %d, got %s", pool, regSet.getName(r));
	}
	def assertDifferent(r: Array<Reg>) {
		for (i < r.length) {
			for (j < r.length) {
				if (i == j) continue;
				if (r[i] == r[j]) return t.fail3("expected, different registers, got %s @ %d and %d",
					regSet.getName(r[i]), i, j);
			}
		}
	}
	def assertAssignments(r: Reg, expected: Array<int>) {
		var g = Vector<int>.new();
		regAlloc.forEachAssignment(r, g.put);
		if (expected.length != g.length) return t.fail2("expected %d assignments, got %d", expected.length, g.length);
		for (e in expected) {
			var found = false;
			for (i < g.length) {
				if (e == g[i]) { found = true; break; }
			}
			if (!found) return t.fail1("expected %s in assignments, but not found", regSet.getName(r));
		}
	}
}

def test_pool0(t: SpcTester) {
	for (r in regSet.regs) {
		t.assertFree(true, r);
	}

	for (r in regSet.regs) {
		t.regAlloc.free(r);
	}

	for (r in regSet.regs) {
		t.assertFree(true, r);
	}
}

def test_pool1(t: SpcTester) {
	// test registers are allocated in order
	for (r in POOL_0.regs) { // TODO: test all pools
		t.assertFree(true, r);
		var g = t.alloc();
		if (r != g) t.t.fail2("expected alloc = %s, got %s", regSet.getName(r), regSet.getName(g));
		t.assertFree(false, r);
	}
}

def test_pool2(t: SpcTester) {
	var ra = t.regAlloc;
	var r1 = t.alloc();
	var r2 = t.alloc();

	ra.free(r2);
	var r3 = t.alloc();
	t.t.assert_eq(r2, r3); // r2 has priority over r3

	ra.free(r1);
	ra.free(r2);
	var r4 = t.alloc();
	t.t.assert_eq(r1, r4); // r1 has priority over r2

	ra.clear();
	t.assertFree(true, RA);
	t.assertFree(true, RB);
	t.assertFree(true, RC);
}

def test_pool3(t: SpcTester) {
	def count = 32;
	var regs = Array<Reg>.new(count + 1);
	for (i < regs.length) regs[i] = Reg(byte.view(i));
	var names = Array<string>.new(count + 1);
	for (i = 1; i < names.length; i++) names[i] = Strings.format1("r%d", i - 1);
	var regSet = RegSet.new(regs, names);

	var poolMap = PoolMap.new(regSet, 1);
	var pool = RegPool32.new(Arrays.range(regs, 1, regs.length));
	var pools = [pool];
	var p = t.regAlloc = RegAlloc.new(poolMap, pools, null);

	for (r in pool.regs) {
		p.clear();
		for (r in pool.regs) if (r != NO_REG) p.assign(r, 10234);
		for (r in pool.regs) t.assertFree(false, r);
		p.free(r);
		t.assertFree(true, r);
		var g = t.alloc();
		if (r != g) t.t.fail2("expected alloc = %d, got %d", r.index, g.index);
	}
}

def test_findSpill(t: SpcTester) {
	var regAlloc = RegAlloc.new(regConfig.poolMap, regConfig.regPools, null);

	var r1 = regAlloc.alloc(ValueKind.I32, 11);
	var r2 = regAlloc.alloc(ValueKind.I32, 22);

	var rs = regAlloc.findSpillCandidate(ValueKind.I32, addOne);
	t.t.assert_ne(r1, r2);
	t.t.assert_eq(r1, rs);
}
def addOne(score: int, reg: Reg, slot: int) -> int {
	return score + 1;
}

def test_alias0(t: SpcTester) {
	var ra = t.regAlloc;

	ra.assign(RA, 99);
	ra.assign(RA, 100);
	ra.assign(RA, 101);
	t.assertAssignments(RA, [99, 100, 101]);

	ra.assign(RA, 102);
	t.assertAssignments(RA, [99, 100, 101, 102]);
	ra.unassign(RA, 102);
	t.assertAssignments(RA, [99, 100, 101]);

	ra.assign(RA, 102);
	ra.assign(RA, 103);
	ra.assign(RA, 104);
	t.assertAssignments(RA, [99, 100, 101, 102, 103, 104]);

	ra.unassign(RA, 101);
	t.assertAssignments(RA, [99, 100, 102, 103, 104]);

	ra.assign(RB, 888);
	t.assertAssignments(RA, [99, 100, 102, 103, 104]);
	t.assertAssignments(RB, [888]);

	ra.unassign(RB, 777);
	t.assertAssignments(RA, [99, 100, 102, 103, 104]);
	t.assertAssignments(RB, [888]);

	ra.unassign(RB, 888);
	t.assertAssignments(RA, [99, 100, 102, 103, 104]);
	t.assertAssignments(RB, []);
	t.assertFree(true, RB);

	ra.unassign(RA, 102);
	ra.unassign(RA, 103);
	ra.unassign(RA, 104);
	t.assertAssignments(RA, [99, 100]);

	ra.unassign(RA, 99);
	ra.unassign(RA, 100);
	t.assertAssignments(RA, []);
	t.assertFree(true, RA);

	ra.unassign(RA, 100);
	t.assertAssignments(RA, []);
	t.assertFree(true, RA);
}

def test_frequency0(t: SpcTester) {
	var ra = t.regAlloc;
	var assert = t.t.assert_eq<int>;

	assert(0, ra.frequency(RA));
	assert(0, ra.frequency(RB));
	assert(0, ra.frequency(RC));
	
	ra.assign(RA, 99);
	assert(1, ra.frequency(RA));
	assert(0, ra.frequency(RB));
	
	ra.assign(RB, 99999);
	assert(1, ra.frequency(RA));
	assert(1, ra.frequency(RB));
	
	ra.assign(RA, 555);
	assert(2, ra.frequency(RA));
	assert(1, ra.frequency(RB));
	
	ra.assign(RA, 777);
	assert(2, ra.frequency(RA));
	assert(1, ra.frequency(RB));

	ra.unassign(RA, 777);
	assert(2, ra.frequency(RA));
	assert(1, ra.frequency(RB));

	ra.unassign(RA, 555);
	assert(1, ra.frequency(RA));
	assert(1, ra.frequency(RB));

	ra.unassign(RA, 99);
	assert(0, ra.frequency(RA));
	assert(1, ra.frequency(RB));

	assert(0, ra.frequency(RC));
}

def trace_alloc(p: SpcRaPool) -> Reg {
	var r = p.alloc();
	if (Trace.test) Trace.OUT.put1("alloc = %s", regSet.getName(r)).outln();
	return r;
}

def test_pool_alloc0(t: SpcTester) {
	for (pool < POOLS.length) {
		var p = SpcRaPool.new(regConfig, pool);
		var alloced = Array<bool>.new(regSet.regs.length);

		// exhaustively allocate from the pool
		for (i < POOLS[pool].length) {
			var r = trace_alloc(p);
			if (r == NO_REG) return t.t.fail("failed allocation");
			if (alloced[r.index]) return t.t.fail("repeated allocation");
			alloced[r.index] = true;
			t.assertInPool(pool, r);
		}

		// No more allocations should succeed.
		for (i < 5) {
			var r = trace_alloc(p);
			if (r != NO_REG) return t.t.fail("allocation succeeded, should fail");
		}
	}
}

def test_pool_alloc1(t: SpcTester) {
	for (pool < POOLS.length) {
		var p = SpcRaPool.new(regConfig, pool);
		for (i < 5 * POOLS[pool].length) {
			var r = trace_alloc(p);
			t.assertInPool(pool, r);
			p.free(r);
		}
	}
}

def test_pool_alloc2(t: SpcTester) {
	for (pool < POOLS.length) {
		var p = SpcRaPool.new(regConfig, pool);
		for (i < 5 * POOLS[pool].length) {
			var r1 = trace_alloc(p), r2 = trace_alloc(p);
			t.assertInPool(pool, r1);
			t.assertInPool(pool, r2);
			t.assertDifferent([r1, r2]);
			if ((i & 1) == 1) { // free in a different order each time
				p.free(r1);
				p.free(r2);
			} else {
				p.free(r2);
				p.free(r1);
			}
		}
	}
}

def test_pool_free0(t: SpcTester) {
	var v = Vector<Reg>.new();
	for (pi < POOLS.length) {
		var pool = POOLS[pi];
		var p = SpcRaPool.new(regConfig, pi);

		// First, exhaust the pool.
		for (i < pool.length) {
			var r = p.alloc();
			v.put(r);
			t.assertInPool(pi, r);
		}
		t.assertDifferent(v.extract());

		// Then free a specific register and realloc it.
		for (i < 3) {
			for (rj in pool) {
				p.free(rj);
				var rk = trace_alloc(p);
				if (rk != rj) return t.t.fail("expected free() and then alloc() to return same");
			}
		}
	}
}

def test_pool_free1(t: SpcTester) {
	var v = Vector<Reg>.new();
	for (pi < POOLS.length) {
		var pool = POOLS[pi];
		var p = SpcRaPool.new(regConfig, pi);

		for (j < 3) {
			// First, exhaust the pool.
			for (i < pool.length) {
				var r = p.alloc();
				v.put(r);
				t.assertInPool(pi, r);
			}
			t.assertDifferent(v.extract());

			// Then free each one manually.
			for (rj in pool) p.free(rj);
		}
	}
}

def test_pool_freeAll(t: SpcTester) {
	var v = Vector<Reg>.new();
	for (pi < POOLS.length) {
		var pool = POOLS[pi];
		var p = SpcRaPool.new(regConfig, pi);

		for (j < 3) {
			// First, exhaust the pool.
			for (i < pool.length) {
				var r = p.alloc();
				v.put(r);
				t.assertInPool(pi, r);
			}
			t.assertDifferent(v.extract());

			// Then free everything,
			var array = Array<int>.new(regSet.regs.length);
			p.freeAllAndResetState(array, -1);

			// And check the array has been reset at those locations too.
			for (rj in pool) {
				if (array[rj.index] != -1) return t.t.fail1("expected array to be cleared for %s", regSet.getName(rj));
			}
		}
	}
}

def test_pool_unfree0(t: SpcTester) {
	var v = Vector<Reg>.new();
	for (pi < POOLS.length) {
		var pool = POOLS[pi];
		// alias a specific register and make sure it doesnt get allocated.
		for (r in pool) {
			var p = SpcRaPool.new(regConfig, pi);
			p.unfree(r);

			for (i < pool.length - 1) {
				var g = trace_alloc(p);
				if (g == r) return t.t.fail1("allocated aliasd register %s", regSet.getName(r));
				v.put(g);
				t.assertInPool(pi, g);
			}
			t.assertDifferent(v.extract());
			for (i < 5) {
				var g = trace_alloc(p);
				if (g != NO_REG) return t.t.fail("should be no more registers");
			}
		}
	}
}
