//@use lib.whamm.WhammReflect;
//@use lib.wizeng.Wizeng;

def PRINT_NAMES = false;

def main() {
}

def name_buffer = Array<byte>.new(128);

export "wasm:exit" def exit() {
	var result = WhammReflect.read_module_path(name_buffer).0;
	Wizeng.putr(result);
	puts(": \n");
	print_defs("Type", -1, WhammReflect.get_num_types, WhammReflect.read_type_def);
	print_defs("Import", -1, WhammReflect.get_num_imports, WhammReflect.read_import_def);
	print_defs("Function", 3, WhammReflect.get_num_functions, WhammReflect.read_function_def);
	print_defs("Table", 4, WhammReflect.get_num_tables, WhammReflect.read_table_def);
	print_defs("Memory", 5, WhammReflect.get_num_memories, WhammReflect.read_memory_def);
	print_defs("Global", 6, WhammReflect.get_num_globals, WhammReflect.read_global_def);
	print_defs("Tag", 13, WhammReflect.get_num_tags, WhammReflect.read_tag_def);
	print_defs("Export", -1, WhammReflect.get_num_exports, WhammReflect.read_export_def);
	print_defs("Elem", -1, WhammReflect.get_num_elements, WhammReflect.read_element_def);
	print_defs("Data", -1, WhammReflect.get_num_data, WhammReflect.read_data_def);
	print_defs("Custom", -1, WhammReflect.get_num_custom_sections, WhammReflect.read_custom_section);
}

def buffer = Array<byte>.new(16);

def puti = Wizeng.puti;
def puts = Wizeng.puts;
def putu(i: u32) { puti(int.!(i)); }

def print_defs(name: string, importable_sect: int, count: void -> u32, read: (Range<byte>, u32) -> (Range<byte>, u32)) {
	var num = count();
	if (num == 0) return;
	puts(name);
	puts("[");
	putu(num);
	puts("]:\n");
	for (i < num) {
		puts(" -");
		if (PRINT_NAMES && importable_sect == 3) {
			var str = WhammReflect.read_function_name(name_buffer, i).0;
			if (str.length > 0) {
				puts(" \"");
				Wizeng.putr(str);
				puts("\"");
			}
		}

		if (importable_sect >= 0) {
			var import_index = WhammReflect.get_import_index(byte.!(importable_sect), i);
			if (import_index >= 0) {
				puts(" imported @");
				puti(import_index);
				puts("\n");
				continue;
			}
		}

		var len = read(buffer, i).1;
		var end = if(len > buffer.length, buffer.length, int.!(len));
		for (j < end) {
			puts(" ");
			putb(buffer[j]);
		}
		if (len > buffer.length) {
			puts("... [");
			putu(len);
			puts(" bytes]");
		}
		puts("\n");
	}
}

def hexMap_u = "0123456789ABCDEF";
def putb(b: byte) {
	wizeng.puts(Pointer.atElement(hexMap_u, b >> 4), 1);
	wizeng.puts(Pointer.atElement(hexMap_u, b & 15), 1);
}
