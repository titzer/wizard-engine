// Copyright 2023 Ben L. Titzer. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

// Global metrics collected by the engine.
component Metrics {
	private def us = "\xCE\xBCs";
	private def bytes = "bytes";
	private var all: List<Metric>;

	// Metrics from initialization/startup.
	def pregen_time_us	= t("pregen:time_us", "Time taken to create pregen asm stubs");
	def pregen_bytes	= m("pregen:bytes", bytes, "Total pregen asm stub bytes");

	// Metrics from binary parsing/loading.
	def load_time_us	= t("load:time_us", "Time taken to load and parse Wasm binary");
	def load_bytes		= m("load:bytes", bytes, "Total Wasm binary bytes loaded");
	def load_time_per_byte	= r("load:time_per_byte", load_time_us, load_bytes,
		"Ratio of load time per input byte");
	
	def validate_time_us	= t("validate:time_us", "Time taken to validate Wasm bytecode");
	def validate_bytes	= m("validate:bytes", bytes, "Total Wasm bytecode bytes validated");
	def validate_time_per_byte = r("validate:time_per_byte", validate_time_us, validate_bytes,
		"Ratio of validation time per input bytecode byte");

	// Metrics from the single-pass compiler.
	def spc_in_bytes	= m("spc:in_bytes", bytes, "Total bytecode bytes input to single-pass compiler");
	def spc_code_bytes	= m("spc:code_bytes", bytes, "Total machine code bytes output by single-pass compiler");
	def spc_data_bytes	= m("spc:data_bytes", bytes, "Total data/metadata bytes output by single-pass compiler");
	def spc_time_us		= t("spc:time_us", "Time taken by single-pass compiler");
	def spc_functions	= m("spc:functions", "functions",
		"Number of functions successfully compiled by single-pass compiler");
	def spc_time_per_byte	= r("spc:time_per_byte", spc_time_us, spc_in_bytes,
		"Ratio of compile time per input bytecode byte");

	// Metrics from executing wasm code.
	def start_time_us	= t("start:time_us", "Time taken to execute wasm module start function(s).");
	def main_time_us	= t("main:time_us", "Time taken to execute wasm module main function(s).");
	def main_time_cycles	= c("main:time_cycles", "Cycles taken to execute wasm module main function(s).");

	// Metrics from probe system.
	var probe_fires		= register(Metric.new("probes:fired", "count", "Number of calls to Probe.fire()"));

	// Keep the metrics in the above-declared order.
	private def unused__ = all = Lists.reverse(all);

	// Enable metrics matching the given pattern.
	def enable(glob: GlobMatcher) {
		for (l = all; l != null; l = l.tail) {
			if (glob.matches(l.head.name)) l.head.enable();
		}
	}
	// Enable all metrics.
	def enableAll() {
		Lists.apply(all, Metric.enable);
	}
	// Report enabled metrics.
	def report() {
		for (l = all; l != null; l = l.tail) {
			var metric = l.head;
			if (metric.enabled) {
				Trace.OUT.puts(metric.name);
				Trace.OUT.pad(' ', 24);
				Trace.OUT.puts(" : ");
				metric.report(Trace.OUT);
				Trace.OUT.sp();
				Trace.OUT.pad(' ', 37);
				Trace.OUT.puts(metric.units);
				Trace.OUT.outln();
			}
		}
	}
	// Create a new metric and add it to the list.
	private def m(name: string, units: string, help: string) -> Metric {
		var r = Metric.new(name, units, help);
		all = List.new(r, all);
		return r;
	}
	// Create a new metric and add it to the list.
	private def t(name: string, help: string) -> TimeMetric {
		var r = TimeMetric.new(name, help);
		all = List.new(r, all);
		return r;
	}
	// Create a new metric and add it to the list.
	private def c(name: string, help: string) -> CyclesMetric {
		var r = CyclesMetric.new(name, help);
		all = List.new(r, all);
		return r;
	}
	// Create a new ratio metric and add it to the list.
	private def r(name: string, n: Metric, d: Metric, help: string) -> Metric {
		var r = RatioMetric.new(name, n, d, help);
		all = List.new(r, all);
		return r;
	}
	private def register(m: Metric) -> Metric {
		all = List.new(m, all);
		return m;
	}
}

// Parses and enables collection/reporting of metrics based on arguments.
component MetricOptions {
	// Parse command-line argument and update metric collection. Return {true} if the
	// argument was matched as a trace flag.
	def parse(arg: string) -> bool {
		if (Strings.equal(arg, "--metrics")) {
			Metrics.enableAll();
			return true;
		}
		var prefix = "--metrics=";
		if (Strings.startsWith(arg, prefix)) {
			var pat = Arrays.range(arg, prefix.length, arg.length);
			var glob = GlobMatcher.new(pat);
			Metrics.enable(glob);
			return true;
		}
		return false;
	}
	// TODO: help
}

// Internal representation of a metric.
class Metric(name: string, units: string, help: string) {
	var enabled: bool;
	var val: u64;

	def enable() {
		this.enabled = true;
	}
	def report(out: StringBuilder) -> StringBuilder {
		return out.putd(val);
	}
}

// Time metrics have an additional {run()} utility method that runs the function and adds to the total.
class TimeMetric extends Metric {
	var count: u64;

	new(name: string, help: string) super(name, Metrics.us, help) { }

	def run<P, R>(func: P -> R, p: P) -> R {
		if (!enabled) return func(p);
		var before_us = System.ticksUs();
		var r = func(p);
		this.val += u32.view(System.ticksUs() - before_us);
		this.count++;
		return r;
	}
	def ticks() -> u64 {
		return u32.view(System.ticksUs());
	}
	def updateDiff(before_us: u64) -> this {
		this.val += u32.view(System.ticksUs() - int.view(before_us));
	}
}

// Cycle metrics have an additional {run()} utility method that runs the function and adds to the total.
class CyclesMetric extends Metric {
	var count: u64;

	new(name: string, help: string) super(name, "cycles", help) { }

	def run<P, R>(func: P -> R, p: P) -> R {
		if (!enabled) return func(p);
		var before_cycles = Target.rdtsc();
		var r = func(p);
		this.val += Target.rdtsc() - before_cycles;
		this.count++;
		return r;
	}
	def ticks() -> u64 {
		return Target.rdtsc();
	}
	def updateDiff(before_cycles: u64) -> this {
		this.val += Target.rdtsc() - before_cycles;
	}
}

// A metric which is the ratio of two other metrics.
class RatioMetric extends Metric {
	def n: Metric;
	def d: Metric;
	
	new(name: string, n, d, help: string)
		super(name, Strings.format2("%s/%s", n.units, d.units), help) {
	}

	def enable() {
		this.enabled = true;
		n.enable();
		d.enable();
	}
	def report(out: StringBuilder) -> StringBuilder {
		var ratio = double.!(n.val) / double.!(d.val);
		var i = long.truncd(ratio);
		out.putd(i);
		out.putc('.');
		for (j < 6) {
			ratio = ratio - double.!(i);
			ratio *= 10d;
			i = long.truncd(ratio);
			out.putd(i);
		}
		return out;
	}
}
