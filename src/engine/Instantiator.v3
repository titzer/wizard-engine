// Copyright 2020 Ben L. Titzer. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

// Extension point for import processing. An {ImportProcessor} lookups or materializes
// an {Exportable} for a given name and import kind. It can use the {binder} as a
// callback to access parts of the module that need polymorphic substitution.
class ImportProcessor(name: string) {
	def preprocess(module: Module, imports: Array<Exportable>) { }
	def processCommand(name: string, args: Array<Exportable>) -> Exportable { return null; }
	def processAbsType(name: string, args: Array<Exportable>, decl: AbsTypeDecl) -> Exportable { return null; }
	def processFunction(name: string, args: Array<Exportable>, decl: FuncDecl) -> Exportable { return null; }
	def processTable(name: string, args: Array<Exportable>, decl: TableDecl) -> Exportable { return null; }
	def processMemory(name: string, args: Array<Exportable>, decl: MemoryDecl) -> Exportable { return null; }
	def processGlobal(name: string, args: Array<Exportable>, decl: GlobalDecl) -> Exportable { return null; }
	def processTag(name: string, args: Array<Exportable>, decl: TagDecl) -> Exportable { return null; }
	def postprocess(instance: Instance) { }
}

def NO_IMPORT_ARGS: Array<Exportable> = [];
// Creates {Instance} objects, given a module and a list of imports.
class Instantiator(extensions: Extension.set, module: Module, var imports: Array<Exportable>, error: ErrorGen) { // TODO: s/error/err
	def instance = Instance.new(module, imports);
	def processors = Vector<ImportProcessor>.new();
	var tiering: ExecutionStrategy;
	var binder: (Decl, Exportable) -> Exportable;		// last binding step
	var trap_reason: TrapReason;
	var import_pos: int;
	var import_info: ImportInfo;

	def run() -> Instance {
		if (tiering != null) tiering.onInstantiateStart(this);
		if (module.imports.length > 0) {
			if (imports == null) imports = Array.new(module.imports.length);
			else if (imports.length < module.imports.length) {
				return fail(Strings.format2("expected %d imports, got %d", module.imports.length, imports.length));
			}
		}
		for (i < processors.length) {
			processors[i].preprocess(module, imports);
		}

		var needSigIds = false;
		for (j < module.decls.length) {
			if (error.error()) return null;
			match (module.decls[j]) {
				Sig(index) => {
					var decl = SigDecl.!(module.heaptypes[index]);
					instance.heaptypes[index] = mapSig(decl); // TODO: respect recursion group
				}
				Struct(index) => {
					var decl = StructDecl.!(module.heaptypes[index]);
					// TODO: substitute type declarations in struct
				}
				Array(index) => {
					var decl = ArrayDecl.!(module.heaptypes[index]);
				}
				AbsType(index) => {
					var decl = module.abstypes[index];
					if (decl.imp != null) {
						var r = imports[decl.imp.import_index];
						var t = importAbsType(decl, r);
						instance.abstypes[index] = t;
						match (t) {
							x: ExportedValueType => instance.abskinds[index] = ValueTypes.kind(x.vt);
							_ => instance.abskinds[index] = ValueKind.REF;
						}
					} else {
						var t = ExportedValueType.new(null, ValueType.Abstract(decl));
						instance.abstypes[index] = t;
					}
					var code: byte;
					match (instance.abskinds[index]) {
						I32 => code = BpTypeCode.I32.code;
						I64 => code = BpTypeCode.I64.code;
						F32 => code = BpTypeCode.F32.code;
						F64 => code = BpTypeCode.F64.code;
						V128 => code = BpTypeCode.V128.code;
						_ => code = BpTypeCode.REF_NULL.code;
					}
					instance.abscodes[index] = code;
				}
				Func(index, sig_index) => {
					var decl = module.functions[index];
					if (decl.imp != null) {
						var r = imports[decl.imp.import_index];
						instance.functions[index] = importFunction(decl, r);
					} else {
						var f = WasmFunction.new(instance, mapFuncDecl(index));
						instance.functions[index] = f;
					}
				}
				Table(index) => {
					var decl = module.tables[index];
					if (decl.imp != null) {
						var r = imports[decl.imp.import_index];
						var t =  importTable(decl, r);
						instance.tables[index] = t;
						if (t != null && t.funcs != null) needSigIds = true;
					} else {
						var t = Table.new(mapType(decl.elemtype), decl);
						if (t.oom) return fail("out of memory allocating table");
						instance.tables[index] = t;
						if (t.funcs != null) needSigIds = true;
					}
				}
				Memory(index) => {
					var decl = module.memories[index];
					if (decl.imp != null) {
						var r = imports[decl.imp.import_index];
						instance.memories[index] = importMemory(decl, r);
					} else {
						var m = Target.newMemory(decl);
						if (m.oom) return fail("out of memory allocating memory");
						instance.memories[index] = m;
					}
				}
				Global(index) => {
					var decl = module.globals[index];
					if (decl.imp != null) {
						var r = imports[decl.imp.import_index];
						instance.globals[index] = importGlobal(decl, r);
					} else {
						var g = Global.new(mapType(decl.valtype), decl);
						instance.globals[index] = g;
						g.value = instance.evalInitExpr(decl.init);
					}
				}
				Tag(index) => {
					var decl = module.tags[index];
					if (decl.imp != null) {
						var r = imports[decl.imp.import_index];
						instance.tags[index] = importTag(decl, r);
					} else {
						instance.tags[index] = Tag.new(getSigDecl(decl.sig_index), decl);
					}
				}
				ImportCommand(index) => {
					var decl = module.imports[index];
					processImport(decl, ImportProcessor.processCommand);
				}
			}
		}
		if (error.error()) return null;
		import_info = null;
		if (needSigIds) {
			for (i < instance.heaptypes.length) {
				match (instance.heaptypes[i]) {
					x: SigDecl => instance.sig_ids[i] = Canon.sigId(x);
					_ => instance.sig_ids[i] = -1;
				}
			}
		}

		// Initialize tables that have default element values.
		for (t in instance.tables) {
			if (!t.decl.has_default_elem) continue;
			var val = instance.evalInitExpr(t.decl.default_elem);
			t.fill(0, val, u32.view(t.elems.length));
		}
		// Load element segments.
		for (i < module.elems.length) {
			var e = module.elems[i];
			match (e.mode) {
				Passive => ;
				Active(index, offset) => {
					loadElems(index, offset, e);
					instance.dropped_elems[i] = true;
					if (error.error()) return null;
				}
				Declarative => {
					instance.dropped_elems[i] = true;
				}
			}
		}
		// Load data segments.
		for (i < module.data.length) {
			var d = module.data[i];
			match (d.mode) {
				Passive => ;
				Active(index, offset) => {
					loadData(index, offset, d);
					instance.dropped_data[i] = true;
					if (error.error()) return null;
				}
				Declarative => {
					instance.dropped_data[i] = true;
				}
			}
		}

		// Organize exports
		var exports = instance.exports;
		for (i < module.exports.length) {
			exports[i] = getDecl(module.exports[i].1);
		}

		for (i < processors.length) {
			processors[i].postprocess(instance);
		}

		if (tiering != null) tiering.onInstantiateFinish(this, error);
		return instance;
	}
	def importAbsType(decl: AbsTypeDecl, r: Exportable) -> ExportedType {
		if (r == null) r = processImport(decl, ImportProcessor.processAbsType(_, _, _, decl));
		if (binder != null) r = binder(decl, r);
		var xt: ExportedType, vt: ValueType;
		match (r) {
			x: HostType => { xt = x; vt = ValueType.Host(x); }
			x: ExportedValueType => { xt = x; vt = x.vt; }
			_ => { fail("expected type"); return null; }
		}
		for (c in decl.assignable) {
			var ct = mapType(c);
			if (!ValueTypes.isAssignable(vt, ct)) {
				fail(Strings.format2("type %q does not match constraint %q", vt.render, ct.render));
			}
		}
		return xt;
	}
	def importFunction(decl: FuncDecl, r: Exportable) -> Function {
		if (r == null) r = processImport(decl, ImportProcessor.processFunction(_, _, _, decl));
		if (binder != null) r = binder(decl, r);
		match (r) {
			func: Function => {
				var expected_sig = getSigDecl(decl.sig_index);
				if (!func.sig.isAssignableSig(expected_sig)) {
					var expected = if(expected_sig == null, StringBuilder.puts(_, "(null)"), expected_sig.render);
					fail(Strings.format2("expected function with signature %q, got %q", expected, func.sig.render));
				}
				return func;
			}
			null => fail("function import not found");
			_ => fail("expected function import");
		}
		return null;
	}
	def importTable(decl: TableDecl, r: Exportable) -> Table {
		if (r == null) r = processImport(decl, ImportProcessor.processTable(_, _, _, decl));
		if (binder != null) r = binder(decl, r);
		match (r) {
			table: Table => {
				if (!checkLimits(table.decl.initial, table.decl.maximum, decl.initial, decl.maximum)) {
					fail("table limits mismatch");
				}
				var expected_type = mapType(decl.elemtype);
				var got_type = table.decl.elemtype;
				if (got_type != expected_type) {  // TODO: proper equality check
					fail("table element type mismatch");
				}
				return table;
			}
		}
		fail("expected table import");
		return null;
	}
	def importMemory(decl: MemoryDecl, r: Exportable) -> Memory {
		if (r == null) r = processImport(decl, ImportProcessor.processMemory(_, _, _, decl));
		if (binder != null) r = binder(decl, r);
		match (r) {
			memory: Memory => {
				if (!checkLimits(memory.size(), memory.decl.maximum, decl.initial, decl.maximum)) {
					fail("memory limits mismatch");
				}
				if (memory.decl.shared != decl.shared) {
					fail("memory sharing mismatch");
				}
				return memory;
			}
		}
		fail("expected memory import");
		return null;
	}
	def importTag(decl: TagDecl, r: Exportable) -> Tag {
		if (r == null) r = processImport(decl, ImportProcessor.processTag(_, _, _, decl));
		if (binder != null) r = binder(decl, r);
		match (r) {
			tag: Tag => {
				var expected_sig = getSigDecl(decl.sig_index);
				if (!tag.sig.isAssignableSig(expected_sig)) {
					var expected = if(expected_sig == null, StringBuilder.puts(_, "(null)"), expected_sig.render);
					fail(Strings.format2("expected tag with signature %q, got %q", expected, tag.sig.render));
				}
				return tag;
			}
			null => fail("tag import not found");
			_ => fail("expected tag import");
		}
		return null;
	}
	def importGlobal(decl: GlobalDecl, r: Exportable) -> Global {
		if (r == null) r = processImport(decl, ImportProcessor.processGlobal(_, _, _, decl));
		if (binder != null) r = binder(decl, r);
		match (r) {
			global: Global => {
				if (decl.mutable != global.decl.mutable) {
					fail("global immutability mismatch");
				}
				var expected_type = mapType(decl.valtype);
				var got_type = global.valtype;
				if (decl.mutable) {
					if (!ValueTypes.isEquivalent(got_type, expected_type)) fail("global type mismatch");
				} else {
					if (!ValueTypes.isAssignable(got_type, expected_type)) 	fail("global type mismatch");
				}
				return global;
			}
		}
		fail("expected global import");
		return null;
	}
	def processImport(decl: Decl, f: (ImportProcessor, string, Array<Exportable>) -> Exportable) -> Exportable {
		var i = decl.imp;
		import_pos = i.import_index;
		var modname = i.module_name, fieldname = i.field_name;
		var args = NO_IMPORT_ARGS;
		if (i.args.length > 0) {
			args = Array<Exportable>.new(i.args.length);
			for (j < args.length) args[j] = getDecl(i.args[j]);
		}
		for (i < processors.length) {
			var p = processors[i];
			if (Strings.equal(modname, p.name)) {
				var r = f(p, fieldname, args);
				if (r != null) {
					imports[decl.imp.import_index] = r;
					return r;
				}
			}
		}
		return null;
	}

	def getDecl(d: Decl) -> Exportable {
		match (d) {
			x: AbsTypeDecl => return instance.abstypes[x.abstype_index];
			x: FuncDecl => return instance.functions[x.func_index];
			x: TableDecl => return instance.tables[x.table_index];
			x: MemoryDecl => return instance.memories[x.memory_index];
			x: GlobalDecl => return instance.globals[x.global_index];
			x: TagDecl => return instance.tags[x.tag_index];
		}
		return null;
	}
	def getSigDecl(sig_index: int) -> SigDecl {
		if (u32.view(sig_index) >= instance.heaptypes.length) return null;
		return SigDecl.!(instance.heaptypes[sig_index]);
	}

	// Substitution utilities.
	def mapFuncDecl(func_index: int) -> FuncDecl {
		var orig = module.functions[func_index];
		if (!module.isAbstract()) return orig;
		var sig_decl = getSigDecl(orig.sig_index);
		if (sig_decl == orig.sig) return orig;
		return orig.dup(sig_decl);
	}
	def mapSig(sig: SigDecl) -> SigDecl {
		if (!module.isAbstract()) return sig;
		if (sig == null) return null;
		var p = Arrays.map(sig.params, mapType);
		var r = Arrays.map(sig.results, mapType);
		var nsig = Canon.sigPR(p, r); // TODO: respect recursion group
		return nsig;
	}
	def mapType(t: ValueType) -> ValueType {
		if (!module.isAbstract()) return t;
		// TODO: memoize results of previous type mappings
		return ValueTypes.map(t, mapAbs).1;
	}
	def mapAbs(t: ValueType) -> (bool, ValueType) {
		match (t) {
			Abstract(it) => {
				var at = instance.abstypes[it.abstype_index];
				match (at) {
					x: HostType => return (true, ValueType.Host(x));
					x: ExportedValueType => return (true, x.vt);
					_ => ; // no change, but constraints may change
				}
			}
			// TODO: struct and arrays
			_ => ;
		}
		return (false, t); // no change
	}

	private def fail(msg: string) -> Instance {
		if (import_info != null) {
			var buf = StringBuilder.new()
				.put3("import #%d(\"%s\".\"%s\"): ", import_pos, import_info.module_name, import_info.field_name)
				.puts(msg);
			msg = buf.toString();
		}
		error.abs(import_pos).set(msg);
		return null;
	}
	private def checkLimits(fmin: u32, fmax: Max, tmin: u32, tmax: Max) -> bool {
		if (fmin < tmin) return false;
		match (tmax) {
			None => return true;
			Set(tmax_val) => match (fmax) {
				None => return false;
				Set(fmax_val) => return fmax_val <= tmax_val;
			}
		}
	}
	private def checkBounds(offset: u32, count: int, length: int, trap: TrapReason) -> bool {
		var end = u64.!(offset) + u64.!(count);
		if (end < offset || end > length) {
			fail("out of bounds in initialization");
			trap_reason = trap;
			return false;
		}
		return true;
	}
	private def checkElemBounds(index: int, offset: InitExpr, details: ElemDetails) {
		var offset = Value.I32.!(instance.evalInitExpr(offset)).val;
		var size = instance.tables[index].elems.length;
		match (details) {
			FuncRefs(vals) => checkBounds(offset, vals.length, size, TrapReason.TABLE_OUT_OF_BOUNDS);
			Exprs(vals) => checkBounds(offset, vals.length, size, TrapReason.TABLE_OUT_OF_BOUNDS);
		}
	}
	private def checkDataBounds(index: int, offset: InitExpr, dlength: int) {
		var offset = Value.I32.!(instance.evalInitExpr(offset)).val;
		var memory = instance.memories[index];
		var mlength = if(memory != null, memory.length());
		checkBounds(offset, dlength, int.view(mlength), TrapReason.MEM_OUT_OF_BOUNDS);
	}
	private def loadElems(index: int, offset: InitExpr, elems: ElemDecl) {
		var dst_offset = Values.v_u(instance.evalInitExpr(offset));
		var table = instance.tables[index];
		var r = table.copyE(instance, dst_offset, elems, 0, u32.view(elems.details.length()));
		if (r != TrapReason.NONE) {
			fail("out of bounds in initialization");
			trap_reason = r;
		}
	}
	private def loadData(index: int, offset: InitExpr, ddecl: DataDecl) {
		var dst_offset = Values.v_u(instance.evalInitExpr(offset));
		var memory = instance.memories[index];
		var r = memory.copyIn(dst_offset, if(ddecl != null, ddecl.data), 0, u32.view(ddecl.data.length));
		if (r != TrapReason.NONE) {
			fail("out of bounds in initialization");
			trap_reason = r;
		}
	}
}