// Copyright 2020 Ben L. Titzer. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

// Value types for all Wasm values. Note that they may be recursive
// mutating the definition of heap types like {StructDecl} and {ArrayDecl}.
type ValueType {
	case BOTTOM;						// Bottom type
	case I32;						// 32-bit integer
	case I64;						// 64-bit integer
	case F32;						// 32-bit float
	case F64;						// 64-bit float
	case V128;						// 128-bit vector
	case Host(host: HostType);				// host-defined types
	case Ref(nullable: bool, heap: HeapType);

	def render(buf: StringBuilder) -> StringBuilder {
		return ValueTypes.render(this, null, buf);
	}
}
type HeapType {
	case ANY;
	case EXTERN;
	case EQ;
	case I31;
	case EXN;
	case NONE;
	case NOFUNC;
	case NOEXTERN;
	case Func(sig: SigDecl);
	case Struct(sdecl: StructDecl);
	case Array(array: ArrayDecl);
	case FwRef(index: int);

	def render(buf: StringBuilder) -> StringBuilder {
		match (this) {
			ANY => return buf.puts("any");
			EXTERN => return buf.puts("extern");
			EQ => return buf.puts("eq");
			I31 => return buf.puts("i31");
			EXN => return buf.puts("exn");
			NONE => return buf.puts("none");
			NOFUNC => return buf.puts("nofunc");
			NOEXTERN => return buf.puts("noextern");
			Func(sig) => return if(sig == null, buf.puts("func"), sig.render(buf));
			Struct(sdecl) => return if(sdecl == null, buf.puts("struct"), sdecl.render(buf));
			Array(array) => return if(array == null, buf.puts("array"), array.render(buf));
			FwRef(index) => return buf.put1("fwref #%d", index);
		}
	}
	def decl() -> HeapTypeDecl {
		match (this) {
			Func(sig) => return sig;
			Struct(sdecl) => return sdecl;
			Array(array) => return array;
			_ => return null;
		}
	}
}

// Utility functions associated with value types.
component ValueTypes {
	def ANYREF = ValueType.Ref(true, HeapType.ANY);
	def EXTERNREF = ValueType.Ref(true, HeapType.EXTERN);
	def EQREF = ValueType.Ref(true, HeapType.EQ);
	def I31REF = ValueType.Ref(true, HeapType.I31);
	def ARRAYREF = ValueType.Ref(true, HeapType.Array(null));
	def NONEREF = ValueType.Ref(true, HeapType.NONE);
	def FUNCREF = ValueType.Ref(true, HeapType.Func(null));
	def STRUCTREF = ValueType.Ref(true, HeapType.Struct(null));
	def EXNREF = ValueType.Ref(true, HeapType.EXN);

	def NULLREF = ValueType.Ref(true, HeapType.NONE);
	def NULLFUNCREF = ValueType.Ref(true, HeapType.NOFUNC);
	def NULLEXTERNREF = ValueType.Ref(true, HeapType.NOEXTERN);

	def ANYREF_NONNULL = ValueType.Ref(false, HeapType.ANY);
	def EXTERNREF_NONNULL = ValueType.Ref(false, HeapType.EXTERN);
	def EQREF_NONNULL = ValueType.Ref(false, HeapType.EQ);
	def I31REF_NONNULL = ValueType.Ref(false, HeapType.I31);
	def FUNCREF_NONNULL = ValueType.Ref(false, HeapType.Func(null));
	def ARRAYREF_NONNULL = ValueType.Ref(false, HeapType.Array(null));

	def NONE = Array<ValueType>.new(0);
	def NO_HEAPTYPES = Array<HeapType>.new(0);

	// Helper utility for a final signature type with no supertypes.
	def newSig = SigDecl.new(true, NO_HEAPTYPES, _, _);

	// Render a value type, careful not to recurse into any type in {seen}.
	def render(vt: ValueType, seen: List<Decl>, buf: StringBuilder) -> StringBuilder {
		var s: string;
		match (vt) {
			BOTTOM => s = "<bottom>";
			I32 => s = "i32";
			I64 => s = "i64";
			F32 => s = "f32";
			F64 => s = "f64";
			V128 => s = "v128";
			Host(host) => return host.render(buf);
			Ref(nullable, heap) => return renderHeapType(buf, seen, nullable, heap);
		}
		return buf.puts(s);
	}
	def renderHeapType(buf: StringBuilder, seen: List<Decl>, nullable: bool, heap: HeapType) -> StringBuilder {
		match (heap) {
			ANY => renderSimpleRef(buf, nullable, "any");
			EXTERN => renderSimpleRef(buf, nullable, "extern");
			EQ => renderSimpleRef(buf, nullable, "eq");
			I31 => renderSimpleRef(buf, nullable, "i31");
			EXN => renderSimpleRef(buf, nullable, "exn");
			NONE => buf.puts(if(nullable, "nullref", "(ref none)"));
			NOFUNC => buf.puts(if(nullable, "nullfuncref", "(ref nofunc)"));
			NOEXTERN => buf.puts(if(nullable, "nullexternref", "(ref noextern)"));
			Func(sig) => {
				if (sig == null) renderSimpleRef(buf, nullable, "func");
				else sig.render2(seen, buf.puts("(ref ")).puts(")");
			}
			Struct(sdecl) => {
				if (sdecl == null) renderSimpleRef(buf, nullable, "struct");
				else renderIndexedRef(buf, nullable, "struct", sdecl.heaptype_index);
			}
			Array(array) => {
				if (array == null) renderSimpleRef(buf, nullable, "struct");
				else renderIndexedRef(buf, nullable, "array", array.heaptype_index);
			}
			FwRef(index) => buf.put1("(fw-ref #%d)", index);
		}
		return buf;
	}
	def renderSimpleRef(buf: StringBuilder, nullable: bool, str: string) {
		if (nullable) buf.puts(str).puts("ref");
		else buf.puts("(ref ").puts(str).puts(")");
	}
	def renderIndexedRef(buf: StringBuilder, nullable: bool, str: string, index: int) {
		buf.puts("(ref ");
		if (nullable) buf.puts("null ");
		buf.put2("%s #%d)", str, index);
	}
	def isNumeric(t: ValueType) -> bool {
		match (t) {
			BOTTOM, I32, I64, F32, F64, V128 => return true;
			_ => return false;
		}
	}
	def isPrimitive(t: ValueType) -> bool {
		match (t) {
			BOTTOM, I32, I64, F32, F64, V128 => return true;
			_ => return false;
		}
	}
	def isRef(t: ValueType) -> bool {
		match (t) {
			BOTTOM, Ref, Host => return true;
			_ => return false;
		}
	}
	def isAssignable(from: ValueType, to: ValueType) -> bool {
		if (from == to) return true;
		if (from == ValueType.BOTTOM) return true;
		var eq = TypeRelation.compare(from, to);
		return eq == TypeEquiv.EQUAL || eq == TypeEquiv.SUB;
	}
	def isCompatibleParamType = ValueType.==; // invariant function types for now
	def isCompatibleReturnType = ValueType.==; // invariant function types for now
	def isAssignableHeap(from: HeapTypeDecl, to: HeapTypeDecl) -> bool {
		if (from == to) return true;
		if (from.canon_id == to.canon_id) return true; // TODO: fix .dup() in unittests
		var eq = TypeRelation.compareSuperTypeChain(from, to);
		return eq == TypeEquiv.EQUAL || eq == TypeEquiv.SUB;
	}
	def hasDefaultValue(t: ValueType) -> bool {
		match (t) {
			Ref(nullable, ht) => return nullable;
			_ => return true;
		}
	}
	def kind(t: ValueType) -> ValueKind {
		match (t) {
			I32 => return ValueKind.I32;
			I64 => return ValueKind.I64;
			F32 => return ValueKind.F32;
			F64 => return ValueKind.F64;
			V128 => return ValueKind.V128;
			_ => return ValueKind.REF;
		}
	}
	def Ref(nullable: bool, ht: HeapTypeDecl) -> ValueType.Ref {
		match (ht) {
			x: StructDecl => return ValueType.Ref(nullable, HeapType.Struct(x));
			x: ArrayDecl => return ValueType.Ref(nullable, HeapType.Array(x));
			x: SigDecl => return ValueType.Ref(nullable, HeapType.Func(x));
			_ => return ValueTypes.ANYREF; // should not happen
		}
	}
	def RefStruct(nullable: bool, x: StructDecl) -> ValueType.Ref {
		return ValueType.Ref(nullable, HeapType.Struct(x));
	}
	def RefArray(nullable: bool, x: ArrayDecl) -> ValueType.Ref {
		return ValueType.Ref(nullable, HeapType.Array(x));
	}
	def RefFunc(nullable: bool, x: SigDecl) -> ValueType.Ref {
		return ValueType.Ref(nullable, HeapType.Func(x));
	}
	def map(t: ValueType, f: ValueType -> (bool, ValueType)) -> (bool, ValueType) {
		var n = f(t);
		if (n.0) return n;
		match (t) {
			Ref(nullable, heap) => {
				match (heap) {
					Func(sig) => {
						if (sig == null) return (false, t);
						var np = mapArray(sig.params, f);
						var nr = mapArray(sig.results, f);
						if (np.0 | nr.0) return (true, ValueType.Ref(nullable, HeapType.Func(Canon.sigPR(np.1, nr.1)))); // TODO
					}
					Struct(sdecl) => { // TODO
					}
					Array(array) => { // TODO
					}
					_ => ;
				}
			}
			_ => ;
		}
		return (false, t);
	}
	def mapArray(array: Array<ValueType>, f: ValueType -> (bool, ValueType)) -> (bool, Array<ValueType>) {
		for (i < array.length) {
			var r = map(array[i], f);
			if (r.0) {
				var narray = Arrays.dup(array);
				narray[i] = r.1;
				for (j = i + 1; j < array.length; j++) {
					narray[j] = map(array[j], f).1;
				}
				return (true, narray);
			}
		}
		return (false, array);
	}
}

// Implementation detail in comparing recursive types. Computes the relation between
// two types that may be mutually recursive, including subtyping between structs,
// arrays, and functions.
enum TypeEquiv {
	UNRELATED,
	SUPER,
	EQUAL,
	SUB
}
component TypeRelation {
	// Compare two types for equality or subtyping.
	def compare(from: ValueType, to: ValueType) -> TypeEquiv {
		if (from == to) return TypeEquiv.EQUAL;
		match (from) {
			Host(fhost) => match (to) {
				Host(thost) => {
					if (fhost.isAssignableTo(to)) return TypeEquiv.SUB;
					if (thost.isAssignableTo(from)) return TypeEquiv.SUPER;
					return TypeEquiv.UNRELATED;
				}
				_ => return if(fhost != null && fhost.isAssignableTo(to), TypeEquiv.SUB, TypeEquiv.UNRELATED);
			}
			Ref(fnullable, fheap) => match (to) {
				Ref(tnullable, theap) => {
					var eq = compareNullable(fnullable, tnullable);
					return combineEq(eq, compareHeapTypes(fheap, theap));
				}
				_ => ;
			}
			_ => ;
		}
		// Left-hand side primitive, but right-hand side could still be abstract or host
		match (to) {
			Host(thost) => if (thost.isAssignableTo(from)) return TypeEquiv.SUPER;
			_ => ;
		}
		return TypeEquiv.UNRELATED;
	}
	def compareHeapTypes(from: HeapType, to: HeapType) -> TypeEquiv {
		if (from == to) return TypeEquiv.EQUAL;
		match (from) {
			ANY => match (to) {
				EQ, I31, Array, Struct, NONE => return TypeEquiv.SUPER;
				_ => ;
			}
			EQ => match (to) {
				ANY => return TypeEquiv.SUB;
				I31, Array, Struct, NONE => return TypeEquiv.SUPER;
				_ => ;
			}
			I31 => match (to) {
				EQ, ANY => return TypeEquiv.SUB;
				NONE => return TypeEquiv.SUPER;
				_ => ;
			}
			Struct(fstruct) => match (to) {
				EQ, ANY => return TypeEquiv.SUB;
				Struct(tstruct) => return compareSuperTypeChain(fstruct, tstruct);
				NONE => return TypeEquiv.SUPER;
				_ => ;
			}
			Array(farray) => match (to) {
				EQ, ANY => return TypeEquiv.SUB;
				Array(tarray) => return compareSuperTypeChain(farray, tarray);
				NONE => return TypeEquiv.SUPER;
				_ => ;
			}
			NONE => match (to) {
				EQ, ANY, Array, Struct => return TypeEquiv.SUB;
				_ => ;
			}
			EXTERN => match (to) {
				NOEXTERN => return TypeEquiv.SUPER;
				_ => ;
			}
			NOEXTERN => match (to) {
				EXTERN => return TypeEquiv.SUB;
				_ => ;
			}
			Func(fsig) => match (to) {
				Func(tsig) => return compareSuperTypeChain(fsig, tsig);
				NOFUNC => return TypeEquiv.SUPER;
				_ => ;
			}
			NOFUNC => match (to) {
				Func => return TypeEquiv.SUB;
				_ => ;
			}
			_ => ;
		}
		return TypeEquiv.UNRELATED;
	}
	def compareSuperTypeChain(from: HeapTypeDecl, to: HeapTypeDecl) -> TypeEquiv {
		if (from == to) return TypeEquiv.EQUAL;
		if (from == null) return TypeEquiv.SUPER;
		if (to == null) return TypeEquiv.SUB;
		for (f = from; f != null; f = f.getFirstSuperType()) {
			if (f == to) return TypeEquiv.SUB;
		}
		for (t = to; t != null; t = t.getFirstSuperType()) {
			if (from == t) return TypeEquiv.SUPER;
		}
		return TypeEquiv.UNRELATED;
	}
	def compareNullable(f: bool, t: bool) -> TypeEquiv {
		return if(f,
			if(t, TypeEquiv.EQUAL, TypeEquiv.SUPER),
			if(t, TypeEquiv.SUB, TypeEquiv.EQUAL));
	}
	def combineEq(a: TypeEquiv, b: TypeEquiv) -> TypeEquiv {
		if (a == TypeEquiv.UNRELATED) return a;
		match (b) {
			UNRELATED => return TypeEquiv.UNRELATED;
			SUPER => return if(a == TypeEquiv.SUB, TypeEquiv.UNRELATED, TypeEquiv.SUPER);
			EQUAL => return a;
			SUB => return if(a == TypeEquiv.SUPER, TypeEquiv.UNRELATED, TypeEquiv.SUB);
		}
	}
}

// Heap type declarations.
class HeapTypeDecl(final: bool, supertypes: Array<HeapType>) extends Decl {
	var hash = 0;
	var heaptype_index = -1;
	var canon_id = -1;
	var recgrp_index = 0;

	def render(buf: StringBuilder) -> StringBuilder;
	def getFirstSuperType() -> HeapTypeDecl {
		if (supertypes == null || supertypes.length < 1) return null;
		match (supertypes[0]) {
			Struct(decl) => return decl;
			Array(decl) => return decl;
			Func(decl) => return decl;
			_ => return null;
		}
	}
	def putUid(buf: StringBuilder) -> StringBuilder {
		if (Trace.uid) buf.put1("@%d", canon_id);
		return buf;
	}
}

// Packedness and mutability for fields and array elements. (ext:gc)
enum Packedness { UNPACKED, PACKED_I8, PACKED_I16 }
type StorageType(valtype: ValueType, pack: Packedness, mutable: bool) { }

// Struct type declaration. (ext:gc)
class StructDecl extends HeapTypeDecl {
	def field_types: Array<StorageType>;
	def defaultable = allHaveDefaultValues(field_types);

	new(final: bool, supertypes: Array<HeapType>, field_types) super(final, supertypes) {}

	def render(buf: StringBuilder) -> StringBuilder {
		return putUid(buf.put1("struct #%d", heaptype_index));
	}
}

// Array type declaration. (ext:gc)
class ArrayDecl extends HeapTypeDecl {
	def elem_types: Array<StorageType>;
	def defaultable = allHaveDefaultValues(elem_types);

	new(final: bool, supertypes: Array<HeapType>, elem_types) super(final, supertypes) {}

	def render(buf: StringBuilder) -> StringBuilder {
		return putUid(buf.put1("array #%d", heaptype_index));
	}
}

// Signature of a function.
class SigDecl extends HeapTypeDecl {
	def params: Array<ValueType>;
	def results: Array<ValueType>;

	new(final: bool, supertypes: Array<HeapType>, params, results) super(final, supertypes) {}

	def isAssignableSig(that: SigDecl) -> bool {
		if (that == null) return false;
		return Arrays.allTrue(this.params, that.params, ValueTypes.isCompatibleParamType) &&
			Arrays.allTrue(this.results, that.results, ValueTypes.isCompatibleReturnType);
	}
	def render(buf: StringBuilder) -> StringBuilder {
		return render2(null, buf);
	}
	def render2(seen: List<Decl>, buf: StringBuilder) -> StringBuilder {
		for (l = seen; l != null; l = l.tail) {
			if (l.head == this) return buf.put1("#%d", heaptype_index);
		}
		seen = List.new(this, seen);
		buf.puts("[");
		for (i < params.length) {
			if (i > 0) buf.puts(" ");
			ValueTypes.render(params[i], seen, buf);
		}
		buf.puts("] -> [");
		for (i < results.length) {
			if (i > 0) buf.puts(" ");
			ValueTypes.render(results[i], seen, buf);
		}
		buf.puts("]");
		return putUid(buf);
	}
	def dup() -> SigDecl {
		var ns = SigDecl.new(final, supertypes, params, results);
		ns.canon_id = this.canon_id;
		return ns;
	}
}

def allHaveDefaultValues(at: Array<StorageType>) -> bool {
	for (t in at) if (!ValueTypes.hasDefaultValue(t.valtype)) return false;
	return true;
}

component HeapTypeDecls {
	def checkSupertypes(pos: int, t: HeapTypeDecl, error: ErrorGen) {
		if (t.supertypes == null || t.supertypes.length < 1) return; // nothing to do
		match (t) {
			x: StructDecl => {
				for (s in t.supertypes) {
					if (!HeapType.Struct.?(s)) return error.abs(pos).IllegalSupertype(x, s);
					var y = HeapType.Struct.!(s).sdecl;
					if (x.field_types.length < y.field_types.length) return error.abs(pos).IllegalSupertype(x, s);
					for (i < y.field_types.length) {
						if (!checkStorageType(x.field_types[i], y.field_types[i])) return error.abs(pos).IllegalSupertype(x, s);
					}
				}
			}
			x: ArrayDecl => {
				for (s in t.supertypes) {
					if (!HeapType.Array.?(s)) return error.abs(pos).IllegalSupertype(x, s);
					var y = HeapType.Array.!(s).array;
					if (!Arrays.allTrue(x.elem_types, y.elem_types, checkStorageType)) return error.abs(pos).IllegalSupertype(x, s);
				}
			}
			x: SigDecl => {
				for (s in t.supertypes) {
					if (!HeapType.Func.?(s)) return error.abs(pos).IllegalSupertype(x, s);
					var y = HeapType.Func.!(s).sig;
					if (!Arrays.allTrue(x.results, y.results, ValueTypes.isAssignable)) return error.abs(pos).IllegalSupertype(x, s);
					if (!Arrays.allTrue(y.params, x.params, ValueTypes.isAssignable)) return error.abs(pos).IllegalSupertype(x, s);
				}
			}
		}
	}
	def checkStorageType(sub: StorageType, sup: StorageType) -> bool {
		if (sub.pack != sup.pack) return false;
		if (sup.mutable) return sub.mutable && sub.valtype == sup.valtype;
		return !sub.mutable && ValueTypes.isAssignable(sub.valtype, sup.valtype);
	}
	def renderStorageType(t: StorageType, buf: StringBuilder) -> StringBuilder {
		match (t.pack) {
			PACKED_I8 => buf.puts("i8");
			PACKED_I16 => buf.puts("i16");
			_ => t.valtype.render(buf);
		}
		return buf;
	}
}