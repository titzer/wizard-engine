// Copyright 2023 Wizard Authors. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

// Implements a simple monitor that prints all memory reads/writes.
def OUT = Trace.OUT;
class MemoryMonitor extends Monitor {
	var reads = Vector<u64>.new();
	var writes = Vector<u64>.new();
	var locations = true;
	var types = false;
	var prevFunc: WasmFunction = null;

	def onParse(module: Module) {
		var mm = ModuleMonitor.new(module);
		mm.beforeMemRead(beforeMemRead);
		mm.beforeMemWrite(beforeMemWrite);
		mm.beforeMemGrow(beforeMemGrow);
	}

	private def putMemIndexAndAddress(index: int, address: u64) {
		if (index > 0) OUT.putx(index).putc(',');
		OUT.putx(address);
	}
	private def putMemIndex(index: int) {
		if (index != 0) OUT.putx(index).putc(',');
	}
	private def putLocation(dynamicLoc: DynamicLoc) {
		if (dynamicLoc.func != prevFunc) {
			OUT.puts(Palette.FUNC);
			dynamicLoc.func.render(OUT);
			OUT.outln();
			prevFunc = dynamicLoc.func;
		}
		OUT.puts(Palette.LNO)
			.rjustify(' ', TraceUtil.renderedLength(OUT, TraceBuilder.put1(_, "+%x", dynamicLoc.pc)), 5);
	}
	private def beforeMemRead(imm: MemArg, dynamicLoc: DynamicLoc, address: u64, size: u64) -> Resumption {
		if (locations) putLocation(dynamicLoc);
		OUT.puts(Palette.SOURCE)
		    .rjustify(' ', TraceUtil.renderedLength(OUT, TraceBuilder.puts(_, "read")), 7)
		    .putc(' ');
		putMemIndexAndAddress(imm.memory_index, address);
		var mem = dynamicLoc.func.instance.memories[imm.memory_index];
		OUT.putc(' ').puts(Palette.DEFAULT);
		for (i < size) {
			var b = mem.read1w(address + i);
			if (b.0 != TrapReason.NONE) {
				OUT.putc('!');
				break;
			}
			OUT.putx_8(b.1);
		}
		OUT.rjustify(' ', 8, 10)
		    .outln();
		return Resumption.Continue;
	}
	private def beforeMemWrite(imm: MemArg, dynamicLoc: DynamicLoc, address: u64, size: u64) -> Resumption {
		if (locations) putLocation(dynamicLoc);
		OUT.puts(Palette.TARGET)
		    .rjustify(' ', TraceUtil.renderedLength(OUT, TraceBuilder.puts(_, "write")), 7)
		    .putc(' ');
		putMemIndexAndAddress(imm.memory_index, address);
		var mem = dynamicLoc.func.instance.memories[imm.memory_index];
		OUT.putc(' ').puts(Palette.DEFAULT);
		var val = dynamicLoc.frame.getFrameAccessor().getOperand(0);
		match (size) {
			1 => match (val) {
				I32(v) => OUT.putx_8(byte.view(v));
				I64(v) => OUT.putx_8(byte.view(v));
				_ => ; // TODO: error
			}
			2 => match (val) {
				I32(v) => OUT.putx_16(u16.view(v));
				I64(v) => OUT.putx_16(u16.view(v));
				_ => ; // TODO: error
			}
			4 => match (val) {
				I32(v) => OUT.putx_32(u32.view(v));
				I64(v) => OUT.putx_32(u32.view(v));
				F32(v) => OUT.putx_32(u32.view(v));
				_ => ; // TODO: error
			}
			8 =>  match (val) {
				I64(v) => OUT.putx_64(u64.view(v));
				F64(v) => OUT.putx_64(u64.view(v));
				_ => ; // TODO: error
			}
			_ => ; // TODO: other sizes
		}
		OUT.rjustify(' ', 8, 10)
		    .outln();
		return Resumption.Continue;
	}
	private def beforeMemGrow(memory_index: u31, dynamicLoc: DynamicLoc, pages: u32) -> Resumption {
		if (locations) putLocation(dynamicLoc);
		OUT.puts(Palette.SUCCESS)
		    .rjustify(' ', TraceUtil.renderedLength(OUT, TraceBuilder.puts(_, "grow")), 7)
		    .putc(' ').puts(Palette.DEFAULT);
		putMemIndex(memory_index);
		OUT.putx_32(pages);
		OUT.putc(' ');
		// TODO: put result
		OUT.rjustify(' ', 9, 14)
		    .outln();
		return Resumption.Continue;
	}
}
