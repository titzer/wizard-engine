// Copyright 2022 Ben L. Titzer. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

// Implements a simple monitor that tracks code coverage.
def monitor_ = MonitorRegistry.add(
	"coverage", "Tallies instruction and basic block coverage.",
	CoverageMonitor.new());
def codeOpt = monitor_.options.addAlias(
	monitor_.options.newFlag("code", null, "emit report with code"),
	"c");

def instrOpt = monitor_.options.addAlias(
	monitor_.options.newFlag("instr", null, "emit report with per-instruction code"),
	"i");

var inBasicBlockMode = false;
var inCodeMode = false;

class CoverageMonitor extends Monitor {
	def entries = Vector<CoverageMonitorProbe>.new();
	def isStartOfBlock = Vector<bool>.new();
	def executed = Vector<bool>.new();

	def onParse(m: Module, err: ErrorGen) {
		inBasicBlockMode = codeOpt.val;
		inCodeMode = codeOpt.val || instrOpt.val;
		for (i < m.functions.length) {
			var f = m.functions[i];
			if (f.imp != null) continue; // skip imported functions
			var offset = executed.length;
			var p = CoverageMonitorProbe.new(m, f, this, offset);
			entries.put(p);
			executed.putn(false, f.cur_bytecode.length);

			isStartOfBlock.putn(false, f.cur_bytecode.length);

			var addToNext = true;
			for (bi = BytecodeIterator.new().reset(f); bi.more(); bi.next()) {
				var op = bi.current();
				if (addToNext) {
					addToNext = false;
					isStartOfBlock[offset + bi.pc] = true;
					if (inBasicBlockMode) Instrumentation.insertLocalProbe(m, f.func_index, bi.pc, p);
				}
				if (!inBasicBlockMode) {
					Instrumentation.insertLocalProbe(m, f.func_index, bi.pc, p);
				}
				match (op) {
					BLOCK, LOOP, TRY, CATCH, CATCH_ALL => {
						isStartOfBlock[offset + bi.pc] = true;
						Instrumentation.insertLocalProbe(m, f.func_index, bi.pc, p);
					}
					BR, BR_IF, BR_TABLE, IF, ELSE, THROW, RETHROW, END => addToNext = true;
					_ => ;
				}
			}
		}
	}
	def onFinish(i: Instance, r: Result) {
		if (inCodeMode) reportCode(out, entries, isStartOfBlock, executed, inBasicBlockMode);
		else report(out, entries, isStartOfBlock, executed);
	}
}
private class CoverageMonitorProbe(module: Module, func: FuncDecl, monitor: CoverageMonitor, start: int) extends Probe {
	def fire(dynamicLoc: DynamicLoc) -> ProbeAction {
		monitor.executed[dynamicLoc.pc + start] = true;
		Instrumentation.removeLocalProbe(module, dynamicLoc.func.decl.func_index, dynamicLoc.pc, this);
		return ProbeAction.Continue;
	}
}

// Report summary
def reportCode(out: TraceBuilder,
		entries: Vector<CoverageMonitorProbe>,
		isStartOfBlock: Vector<bool>,
		executed: Vector<bool>,
		inBasicBlockMode: bool) {
	var lowCoverageThreshold = 75; // in %, for highlighting low coverages
	var prev: FuncDecl;
	var tracer = InstrTracer.new(), d = DataReader.new([]);
	var blockCovered = false;
	var it = BytecodeIterator.new();

	for (j < entries.length) {
		var e = entries[j];
		out.beginColor(Color.FUNC).put1("func %q:", e.func.render(e.module.names, _)).endColors().ln();
		for (bi = it.reset(e.func); bi.more(); bi.next()) {
			bi.current();
			d.reset(e.func.orig_bytecode, bi.pc, e.func.orig_bytecode.length);
			if (inBasicBlockMode) {
				var isStart = isStartOfBlock[e.start + bi.pc];
				if (isStart) {
					blockCovered = executed[e.start + bi.pc];
					if (blockCovered) {
						out.beginColor(Color.LINE)
							.mark()
							.put1("+%d", bi.pc)
							.rjustify_mark(5)
							.beginColor(Color.COVERED)
							.puts(" -->")
							.endColors();
					} else {
						out.beginColor(Color.UNREACHABLE)
							.mark()
							.put1("+%d", bi.pc)
							.rjustify_mark(5);
					}
				} else {
					if (!blockCovered) out.beginColor(Color.UNREACHABLE);
					else out.endColors();
					out.puts("    -");
				}
				if (blockCovered) {
					out.beginColor(Color.COVERED).puts(if(isStart, "", "    "));
				} else {
					out.puts("    ");
				}
				out.endColors().puts("  ");
				if (!blockCovered) out.beginColor(Color.UNREACHABLE);
				else if (isStart) out.beginColor(Color.COVERED_FG).beginColor(Color.COVERED_BG);
				tracer.putInstr(out, e.module, d);
				out.endColors().ln();
			} else {
				var byteCovered = executed[e.start + bi.pc];
				if (byteCovered) out.beginColor(Color.LINE);
				else out.beginColor(Color.UNREACHABLE); 
				out.mark()
					.put1("+%d", bi.pc)
					.rjustify_mark(5);
				out.puts("  ");
				if (!byteCovered) out.puts("  ").beginColor(Color.UNREACHABLE);
				else out.beginColor(Color.COVERED).puts("* ").beginColor(Color.COVERED_FG).beginColor(Color.COVERED_BG);
				tracer.putInstr(out, e.module, d);
				out.endColors().ln();
			}
		}
	}
}
def report(out: TraceBuilder,
		entries: Vector<CoverageMonitorProbe>,
		isStartOfBlock: Vector<bool>,
		executed: Vector<bool>) {
	var lowCoverageThreshold = 75; // in %, for highlighting low coverages
	var prev: FuncDecl;
	var tracer = InstrTracer.new(), d = DataReader.new([]);
	var blockCovered = false;
	var it = BytecodeIterator.new();
	var instrsCovered = 0;
	var totalInstrs = 0;
	var blocksCovered = 0;
	var totalBlocks = 0;
	for (j < entries.length) {
		var e = entries[j];
		for (bi = it.reset(e.func); bi.more(); bi.next()) {
			bi.current();
			d.reset(e.func.orig_bytecode, bi.pc, e.func.orig_bytecode.length);
			if (isStartOfBlock[e.start + bi.pc]) {
				totalBlocks++;
				if (executed[e.start + bi.pc]) blocksCovered++;
			}
			totalInstrs++;
			if (executed[e.start + bi.pc]) instrsCovered++;
		}
	}
	var instrCovPercentage = 100 * instrsCovered / totalInstrs;
	var blockCovPercentage = 100 * blocksCovered / totalBlocks;
	out.puts("Instr coverage:\t")
		.beginColor(Color.ACTIVE)
		.putd(instrsCovered)
		.endColors()
		.put1(" / %d ", totalInstrs);
	out.puts("(").beginColor(Color.SUCCESS);
	if (instrCovPercentage < 100) out.beginColor(Color.WARN);
	if (instrCovPercentage < lowCoverageThreshold) out.beginColor(Color.DANGER);
	TraceUtil.renderPercent(out, instrsCovered, totalInstrs, 1);
	out.endColors().puts(")").ln()
		.puts("Block coverage:\t")
		.beginColor(Color.ACTIVE)
		.putd(blocksCovered)
		.endColors()
		.put1(" / %d ", totalBlocks);
	out.puts("(").beginColor(Color.SUCCESS);
	if (blockCovPercentage < 100) out.beginColor(Color.WARN);
	if (blockCovPercentage < lowCoverageThreshold) out.beginColor(Color.DANGER);
	TraceUtil.renderPercent(out, blocksCovered, totalBlocks, 1);
	out.endColors().puts(")").ln();
}
