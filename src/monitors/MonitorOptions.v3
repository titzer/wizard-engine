// Copyright 2022 Ben L. Titzer. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

// Parses and updates monitor options based on arguments.
component MonitorOptions {
	// Debug verification/check options.
	def enableCheckMonitors = false;

	var group = OptionsRegistry.newGroup("MONITOR", printHelp);
	def CSV = group.newBoolOption("csv", false, "Enable comma-separated value output for monitors.");
	def X_ = OptionsRegistry.addParseFunc(parse);
	
	private var monitors = Vector<Monitor>.new();

	// Parse a command-line argument, configuring and updating monitors. Return {true}
	// if the argument was matched and parsed successfully.
	def parse(arg: string, err: ErrorGen) -> bool {
		if (!Strings.startsWith(arg, "--monitors=")) return false;

		// Debug monitors.
		if (enableCheckMonitors) {
			if (!addMonitor("debug:stackChecker", null, err)) return false;
		}

		var s = 11, pos = s;
		var inArg = false;
		while (pos < arg.length) {
			match (arg[pos]) {
				'{' => {
					var name = Arrays.range(arg, s, pos);
					var args_start = ++pos, args_end = arg.length;
					while (pos < arg.length) {
						var ch = arg[pos++];
						if (ch == '}') { args_end = pos-1; break; }
					}
					var args = Arrays.range(arg, args_start, args_end);
					if (!addMonitor(name, args, err)) return false;
					s = pos;
				}
				',' => {
					var name = Arrays.range(arg, s, pos);
					if (!addMonitor(name, null, err)) return false;
					s = ++pos;
				}
				_ => pos++;
			}
		}
		if (s < arg.length) return addMonitor(Arrays.range(arg, s, arg.length), null, err);

		return !err.error();
	}
	private def addMonitor(name: string, args: string, err: ErrorGen) -> bool {
		def WASM_EXT = ".wasm";
		if (Strings.endsWith(name, WASM_EXT)) {
			def moduleNames = Strings.splitOnChar(name, byte.==('+', _), -1);
			// Make sure all module names end in '.wasm'
			for (name in moduleNames) {
				if (!Strings.endsWith(name, WASM_EXT)) {
					err.InvalidFileType(name, WASM_EXT);
					return false;
				}
			}

			// Load all modules in the group and pass them to the WhammMonitor
			// to be instantiated for each application module.
			var engine = Engine.new();
			engine.extensions = Extension.set.all;
			var tiering_override = if(SpcTuning.compileWhammModules, ExecuteOptions.getModeOrDefault("jit"), ExecuteOptions.getModeOrDefault("int"));
			var named_modules = Array<NamedModule>.new(moduleNames.length);
			for (i < moduleNames.length) {
				var name = moduleNames[i];
				var module = engine.loadWasmFile(name, err, tiering_override, null);
				if (err.error()) return false; // Something bad happened during load
				var nm = NamedModule(name, PathUtil.extractName(name, false), module);
				named_modules[i] = nm;
			}
			monitors.put(WhammMonitor.new(named_modules));
			return true;
		}
		var entry = MonitorRegistry.lookupName(name);
		if (entry != null) {
			if (args != null) {
				parseMonitorArgs(entry, args);
				var msg = entry.monitor.configure(args);
				if (msg != null) Trace.OUT.put2("%s: %s", name, msg).ln();
			}
			monitors.put(entry.monitor);
		}
		return true;
	}
	private def parseMonitorArgs(entry: MonitorRegistryEntry, args: string) {
		var s = 0, pos = 0;
		var arg: string;
		while (pos < args.length) {
			match (args[pos]) {
				',' => {
					arg = Arrays.prepend('-', Arrays.range(args, s, pos));
					var options = entry.options.o;
					options.setOption(options.parseOption(arg));
					s = ++pos;
				}
				_ => pos++;
			}
		}
		if (s < args.length) {
			arg = Arrays.prepend('-', Arrays.range(args, s, args.length));
			var options = entry.options.o;
			var result = options.setOption(options.parseOption(arg));
		}
	}
	// Get the monitors, if any, configured by the above.
	def getMonitors() -> Vector<Monitor> {
		return monitors;
	}
	def printHelp(out: TraceBuilder) {
		var H = out.putsln, L = out.ln;
		H("Wizard offers instrumentation and dynamic analyses of Wasm programs via monitors.");
		H("Multiple monitors can be enabled simultaneously using the command-line flag.");
		L();
		H("   --monitors=<monitor>{<opts>}[,<monitor>{<opts>}]");
		L();
		H("Each monitor comes with its own set of optional arguments and their modifiers.");
		H("This build of the Wizard engine supports the following monitors:");
		L();

		MonitorRegistry.printHelp();

		L();
		H("Additional global monitor options include:");
		L();
		group.print(out, false);
		
		H("Example usage:");
		L();
		H("   --monitors=loops,coverage{code=instr}");

		L();
		H("Wizard also supports Whamm monitors in the form of Wasm modules and supports");
		H("dynamically linking monitor modules together by specifying their paths joined ");
		H("with the '+' character, with the monitor module first in the list.");
		L();
		H("Example usage:");
		L();
		H("   --monitors=path/to/whamm-monitor.wasm+path/to/lib.wasm");
		L();
	}
}
