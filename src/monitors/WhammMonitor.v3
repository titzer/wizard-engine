// Copyright 2024 Wizard authors. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

// This class implements a monitor that is implemented in WebAssembly, i.e. as a Whamm
// module. Such modules have exported functions whose names describe how to attach
// behavior to events.

// Event names supported:
//   wasm:exit
//   wasm:opcode:<mnemonic>			Before matching opcodes execute

// TODO:
//  - define frame accessor methods
//  - externref in Virgil
//  - allow bind-time callbacks to return void (or multivalue?)
class WhammMonitor(named_modules: Array<NamedModule>) extends Monitor {
	def whamm = named_modules[0].module; // whamm monitor module is first in group
	var opcodePatterns: Array<List<(Opcode, Array<WhammParam>, FuncDecl, WhammPredicate)>>;
	var funcEntries: List<(Array<WhammParam>, FuncDecl, WhammPredicate)>;
	var funcExits: List<(Array<WhammParam>, FuncDecl, WhammPredicate)>;
	var whammInstances: List<WhammInstance>; // XXX: leaky
	var err: ErrorGen;

	new() {
		if (Trace.whamm) Trace.OUT.put1("parsing whamm monitor: %s", whamm.filename).ln();
		for (i < whamm.exports.length) {
			var e = whamm.exports[i], name = e.0, decl = e.1;
			if (!FuncDecl.?(decl)) continue;
			if (Trace.whamm) Trace.OUT.put2("  export %d: \"%s\" = ", i, name);
			var func = FuncDecl.!(decl);
			var r = TextReader.new(whamm.filename, name);
			var t = Whamm.parseProbePattern(r), params = t.1, pred = t.2;
			if (!r.ok) {
				// parse error
				Trace.OUT.put1("whamm warning: export[%d] function name is invalid event pattern", i).ln();
				r.renderCurrentLineWithCaret(Trace.OUT, r.error_column);
				Trace.OUT.sp().puts(r.error_msg);
				Trace.OUT.ln();
				if (Trace.fatal) System.error("MonitorError", "failed parsing whamm monitor");
				continue;
			}
			if (Trace.whamm) {
				Trace.OUT.put1("matched: %q", t.0.render);
				if (params.length > 0) Trace.OUT.putTuple(params, WhammParam.render);
				Trace.OUT.ln();
			}
			match (t.0) {
				Null => ; // do nothing
				FuncEntry => {
					funcEntries = List.new((params, func, pred), funcEntries);
				}
				FuncExit => {
					funcExits = List.new((params, func, pred), funcExits);
					addOpcodePattern(Opcode.RETURN, params, func, pred);
				}
				Opcode(op) => {
					addOpcodePattern(op, params, func, pred);
				}
			}
		}
		if (opcodePatterns != null) {
			for (i < opcodePatterns.length) {
				opcodePatterns[i] = Lists.reverse(opcodePatterns[i]);
			}
		}
		funcEntries = Lists.reverse(funcEntries);
		funcExits = Lists.reverse(funcExits);
	}
	private def addOpcodePattern(op: Opcode, params: Array<WhammParam>, func: FuncDecl, pred: WhammPredicate) {
		if (opcodePatterns == null) opcodePatterns = Array.new(Opcode.count);
		opcodePatterns[op.tag] = List.new((op, params, func, pred), opcodePatterns[op.tag]); // XXX: probes added in reverse order
	}
	// monitors called on parsed modules
	def onParse(module: Module, err: ErrorGen) {
		if (opcodePatterns == null && funcEntries == null && funcExits == null) return; // nothing to do

		var before_us = Metrics.whamm_bind_time_us.ticks();
		this.err = err;
		var wi = getWhammInstance(module);
		if (wi == null) return; // an error occurred

		var extensions = Extension.set.all;
		var cv = CodeValidator.new(extensions, Limits.new().set(extensions), module, err);
		var binder = WhammVarBinder.new(wi, err);
		cv.probe = validatorCallback(binder, _, _, _);
		// Iterate over the (non-imported) functions in the module.
		for (i < module.functions.length) {
			var func = module.functions[i];
			if (func.imp != null) continue;
			// If there are any opcode patterns to bind, validate the function using the validator callback.
			if (opcodePatterns != null) cv.validateFunc(func);
			cv.opcode_pos = 0;
			// If there are any function entries to bind, do them explicitly.
			for (l = funcEntries; l != null; l = l.tail) {
				def wf = binder.wi.instance.functions[l.head.1.func_index];
				matchProbe(binder, cv, 0, Opcode.INVALID, l.head.0, wf, l.head.2);
			}
			// If there are any function exits to bind, handle the function end bytecode explicitly.
			var last_pc = func.orig_bytecode.length - 1;
			cv.opcode_pos = last_pc;
			for (l = funcExits; l != null; l = l.tail) {
				def wf = binder.wi.instance.functions[l.head.1.func_index];
				matchProbe(binder, cv, last_pc, Opcode.INVALID, l.head.0, wf, l.head.2);
			}
		}
		Metrics.whamm_bind_time_us.updateDiff(before_us);
	}
	private def validatorCallback(binder: WhammVarBinder, cv: CodeValidator, opcode_pos: int, isFuncEnd: bool, opcode: Opcode) {
		var bp = opcodePatterns[opcode.tag];
		for (l = bp; l != null; l = l.tail) {
			def wf = binder.wi.instance.functions[l.head.2.func_index];
			matchProbe(binder, cv, opcode_pos, isFuncEnd, opcode, l.head.1, wf, l.head.3);
		}
	}
	private def matchProbe(binder: WhammVarBinder, cv: CodeValidator, opcode_pos: int, isFuncEnd: bool, opcode: Opcode, params: Array<WhammParam>, wf: Function, pred: WhammPredicate) {
		var func = cv.func;
		if (Trace.whamm) Trace.OUT.put3("  match func %q+%d: %s", func.render(cv.module.names, _), opcode_pos, opcode.mnemonic);
		binder.probe_id = -1;
		// check static predicate
		if (pred != null) {
			match (binder.evalCall(pred.call.target, pred.call.params, cv, isFuncEnd, opcode)) {
				OK(t, v) => {
					match (v) {
						I32(i) => if (i == 0) {
							if (Trace.whamm) Trace.OUT.puts(" => probe not inserted (predicate)").ln();
							return;
						}
						_ => {
							err.at(cv.parser.decoder).WhammProbeError("whamm probe", "type error in predicate return value");
							return;
						}
					}
				}
				Fail => return;
			}
		}
		def args = binder.bindParams(cv, wf.sig.params, isFuncEnd, params, opcode, false);
		if (args != null) {
			var probe = WhammProbe.new(wf, args);
			Instrumentation.insertLocalProbe(cv.module, func.func_index, opcode_pos, probe);
			if (binder.probe_id >= 0) binder.wi.app.dyn_probes.set(binder.probe_id, (func.func_index, opcode_pos, probe));
			if (Trace.whamm) Trace.OUT.ln();
		} else {
			if (Trace.whamm) Trace.OUT.puts("    => probe not inserted (no match error)").ln();
		}
	}
	private def getWhammInstance(module: Module) -> WhammInstance {
		for (l = whammInstances; l != null; l = l.tail) {
			if (l.head.app == module) return l.head;
		}

		if (Trace.whamm) Trace.OUT.put2("Instantiating whamm module %s for %s", whamm.filename, module.filename).ln();
		var err = ErrorGen.new(whamm.filename);
		var instrumentor = if(module.outline != null, DynInstrModule.new("whamm:dyninstr", module));
		var reflector = if(module.outline != null, ReflectorModule.new("whamm:reflect", module));

		// Perform the linking for Whamm monitor support libraries.
		var engine = Engine.new();
		engine.extensions = Extension.set.all;

		var linker = ModuleLinker.new(engine, [], err);
		linker.binder = if(BasicTracing.moduleFilter != null, ImportTracer.new(BasicTracing.moduleFilter).wrap);
		linker.named_modules.putr(named_modules[1 ...]);
		linker.named_modules.put(named_modules[0]);  		// whamm module goes at the end of the linking list
		linker.lookup.put(ModuleRegistry.get);					// Expose entire module registry
		linker.addImportProcessor(ModuleRegistry.getUnexposed("wizeng"));	// Expose wizeng explicitly
		linker.addImportProcessor(reflector);					// Expose the reflector module
		linker.addImportProcessor(instrumentor);				// Expose the instrumentor module
		var all_instantiators = linker.link();
		if (!err.ok()) {
			var index = Ints.max(err.index, named_modules.length - 1);
			ErrorBuilder.new()
				.render(err.error_code,
						named_modules[index].module.filename,
						err.section,
						err.error_index,
						err.error_pos,
						err.error_msg)
				.exit(6);
			return null;
		}

		// Get the whamm instantiator (the last one)
		var i = all_instantiators[all_instantiators.length - 1];
		var instance = i.run();
		if (!err.ok()) return ErrorBuilder.new()
			.render(err.error_code, whamm.filename, err.section, err.error_index, err.error_pos, err.error_msg)
			.exit(null);
		var wi = WhammInstance.new(module, instance);
		whammInstances = List.new(wi, whammInstances);
		return wi;
	}

	def onFinish(i: Instance, r: Result) {
		def wi = getWhammInstance(i.module);
		if (wi == null) return;
		def exports = wi.instance.exports;
		// TODO make wasm:exit part of parsing whamm module
		for (i < exports.length) {
			var export_name = wi.instance.module.exports[i].0;
			if (Strings.equal("wasm:exit", export_name)) {
				var lookupR = wi.instance.exports[i];
				if (Function.?(lookupR)) return invokeWasmExit(wi, Function.!(lookupR));
			}
		}
	}
	def invokeWasmExit(wi: WhammInstance, func: Function) {
		// Record the time taken executing the reporting logic.
		if (Trace.whamm) Trace.OUT.puts("wasm:exit call!").ln();
		var before_us = Metrics.whamm_report_time_us.ticks();
		def result = Execute.call(func, Values.NONE); // XXX: runs on a new stack
		match (result) {
			Throw(thrown) => {
				if (Trace.whamm) Trace.OUT.puts("wasm:exit threw an error").ln();
				TraceUtil.renderThrowable(Trace.OUT, thrown);
			}
			_ => {} // ignore
		}
		Metrics.whamm_report_time_us.updateDiff(before_us);
	}
}

