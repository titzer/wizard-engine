// Copyright 2024 Wizard authors. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

// This class implements a monitor that is implemented in WebAssembly, i.e. as a Whamm
// module. Such modules have exported functions whose names describe how to attach
// behavior to events.

// Event names supported:
//   wasm:exit
//   wasm:opcode:<mnemonic>			Before matching opcodes execute

// TODO:
//  - define frame accessor methods
//  - externref in Virgil
//  - allow bind-time callbacks to return void (or multivalue?)
class WhammMonitor(named_modules: Array<NamedModule>) extends Monitor {
	def whamm = named_modules[0].module; // whamm monitor module is first in group
	var opcodePatterns: Array<List<(Opcode, Array<WhammParam>, FuncDecl, WhammPredicate)>>;
	var funcPatterns: Array<List<(FuncMode, Array<WhammParam>, FuncDecl, WhammPredicate)>>;
	var whammInstances: List<WhammInstance>; // XXX: leaky
	var err: ErrorGen;

	new() {
		if (Trace.whamm) Trace.OUT.put1("parsing whamm monitor: %s", whamm.filename).ln();
		for (i < whamm.exports.length) {
			var e = whamm.exports[i], name = e.0, decl = e.1;
			if (!FuncDecl.?(decl)) continue;
			var func = FuncDecl.!(decl);
			if (Trace.whamm) {
				Trace.OUT.put2("export %d: \"%s\" = ", i, name).ln();
				Trace.OUT.puts("\twith func sig: ");
				func.sig.render(Trace.OUT).ln();
			}

			var r = TextReader.new(whamm.filename, name);
			var t = Whamm.parseProbePattern(r), evt = t.0, params = t.1, pred = t.2;
			if (!r.ok) {
				// parse error
				Trace.OUT.put1("whamm warning: export[%d] function name is invalid rule pattern", i).ln();
				r.renderCurrentLineWithCaret(Trace.OUT, r.error_column);
				Trace.OUT.sp().puts(r.error_msg);
				Trace.OUT.ln();
				if (Trace.fatal) System.error("MonitorError", "failed parsing whamm monitor");
			} else if (t.0 == WhammEvent.NULL) {
				// no events matched
				if (Trace.whamm) Trace.OUT.puts("\tno events matched").ln();
			} else {
				// ok, add pattern to table
				if (Trace.whamm) {
					Trace.OUT.puts("\tmatched event: \n\t\t");
					evt.render(Trace.OUT);
					Trace.OUT.puts(", with params: ");
					if (params.length > 0) Trace.OUT.putTuple(params, WhammParam.render).ln();
				}

				match (evt) {
					Opcode(op) => {
						if (opcodePatterns == null) opcodePatterns = Array.new(Opcode.count);
						opcodePatterns[op.tag] = List.new((op, params, func, pred), opcodePatterns[op.tag]); // XXX: probes added in reverse order
					}
					Func(mode) => {
						if (funcPatterns == null) funcPatterns = Array.new(FuncMode.count);
						funcPatterns[mode.tag] = List.new((mode, params, func, pred), funcPatterns[mode.tag]); // XXX: probes added in reverse order
					}
					_ => {}
				}
			}
		}
		if (opcodePatterns != null) {
			for (i < opcodePatterns.length) {
				opcodePatterns[i] = Lists.reverse(opcodePatterns[i]);
			}
		}
		if (funcPatterns != null) {
			for (i < funcPatterns.length) {
				funcPatterns[i] = Lists.reverse(funcPatterns[i]);
			}
		}
	}
	// monitors called on parsed modules
	def onParse(module: Module, err: ErrorGen) {
		var before_us = Metrics.whamm_bind_time_us.ticks();
		this.err = err;
		// What extensions?
		var wi = getWhammInstance(module);
		if (wi == null) return;
		if (opcodePatterns == null && funcPatterns == null) return;
		// iterate over the functions in the module
		for (i < module.functions.length) {
			var func = module.functions[i];
			if (func.imp != null) continue; // imported

			var extensions = Extension.set.all;
			var cv = CodeValidator.new(extensions, Limits.new().set(extensions), module, err);
			cv.probe = bytecodeProbe(module, wi, func, _, _, _, _);
			cv.validateFunc(func);
		}
		Metrics.whamm_bind_time_us.updateDiff(before_us);
	}
	def bytecodeProbe(module: Module, wi: WhammInstance, func: FuncDecl, cv: CodeValidator, opcode_idx: int, opcode_pos: int, opcode: Opcode) {
		var binder = WhammVarBinder.new(wi);
		def handler = handleProbe(binder, _, _, _, module, wi, func, cv, opcode_pos, opcode);

		if (funcPatterns != null) {
			if (opcode_idx == 0) {
				// for every function ENTRY probe
				for (probes = funcPatterns[FuncMode.Entry.tag]; probes != null; probes = probes.tail) {
					for (p = probes; p != null; p = p.tail) {
						if (Trace.whamm) Trace.OUT.put2("Func: %s, %d", p.head.0.mnemonic, p.head.0.tag).ln();
						handler(p.head.1, p.head.2, p.head.3);
					}
				}
			}

			if (isFuncExit()) {
				// for every function EXIT probe
				for (probes = funcPatterns[FuncMode.Exit.tag]; probes != null; probes = probes.tail) {
					for (p = probes; p != null; p = p.tail) {
						if (Trace.whamm) Trace.OUT.put2("Func: %s, %d", p.head.0.mnemonic, p.head.0.tag).ln();
						handler(p.head.1, p.head.2, p.head.3);
					}
				}
			}
		}

		if (opcodePatterns != null) {
			// for every matched OPCODE probe
			for (probes = opcodePatterns[opcode.tag]; probes != null; probes = probes.tail) {
				for (p = probes; p != null; p = p.tail) {
					if (Trace.whamm) Trace.OUT.put2("Opcode: %s, %d", p.head.0.mnemonic, p.head.0.tag).ln();
					handler(p.head.1, p.head.2, p.head.3);
				}
			}
		}
	}
	private def handleProbe(binder: WhammVarBinder, params: Array<WhammParam>, callback: FuncDecl, pred: WhammPredicate, module: Module, wi: WhammInstance, func: FuncDecl, cv: CodeValidator, opcode_pos: int, opcode: Opcode) {
		if (Trace.whamm) Trace.OUT.put3("\tmatch func %q+%d: %s", func.render(module.names, _), opcode_pos, opcode.mnemonic).ln();
	
		binder.probe_id = -1;
		def wf = wi.instance.functions[callback.func_index];

		// check static predicate
		if (pred != null) {
			match (binder.evalCall(pred.call.target, pred.call.params, cv, opcode, err)) {
				OK(t, v) => {
					match (v) {
						I32(i) => if (i == 0) {
							if (Trace.whamm) Trace.OUT.puts(" => probe not inserted (predicate)").ln();
							return;
						}
						_ => {
							err.at(cv.parser.decoder).WhammProbeError("whamm probe", "type error in predicate return value");
							return;
						}
					}
				}
				Fail => return;
			}
		}
		def args = binder.bindParams(cv, wf, params, opcode, false, err);
		if (args != null) {
			var probe = WhammProbe.new(wf, args);
			Instrumentation.insertLocalProbe(module, func.func_index, opcode_pos, probe);
			if (binder.probe_id >= 0) wi.app.dyn_probes.set(binder.probe_id, (func.func_index, opcode_pos, probe));
		} else {
			if (Trace.whamm) Trace.OUT.puts("\t\t=> probe not inserted (no match error)").ln();
		}
	}
	private def isFuncExit() -> bool {
		// not implemented yet!!
		return false;
	}
	private def getWhammInstance(module: Module) -> WhammInstance {
		for (l = whammInstances; l != null; l = l.tail) {
			if (l.head.app == module) return l.head;
		}

		if (Trace.whamm) Trace.OUT.put2("Instantiating whamm module %s for %s", whamm.filename, module.filename).ln();
		var err = ErrorGen.new(whamm.filename);
		var instrumentor = if(module.outline != null, DynInstrModule.new("whamm:dyninstr", module));
		var reflector = if(module.outline != null, ReflectorModule.new("whamm:reflect", module));

		// Perform the linking for Whamm monitor support libraries.
		var engine = Engine.new();
		engine.extensions = Extension.set.all;

		var linker = ModuleLinker.new(engine, [], err);
		linker.binder = if(BasicTracing.moduleFilter != null, ImportTracer.new(BasicTracing.moduleFilter).wrap);
		linker.named_modules.putr(named_modules[1 ...]);
		linker.named_modules.put(named_modules[0]);  		// whamm module goes at the end of the linking list
		linker.lookup.put(ModuleRegistry.get);					// Expose entire module registry
		linker.addImportProcessor(ModuleRegistry.getUnexposed("wizeng"));	// Expose wizeng explicitly
		linker.addImportProcessor(reflector);					// Expose the reflector module
		linker.addImportProcessor(instrumentor);				// Expose the instrumentor module
		var all_instantiators = linker.link();
		if (!err.ok()) {
			var index = Ints.max(err.index, named_modules.length - 1);
			ErrorBuilder.new()
				.render(err.error_code,
						named_modules[index].module.filename,
						err.section,
						err.error_index,
						err.error_pos,
						err.error_msg)
				.exit(6);
			return null;
		}

		// Get the whamm instantiator (the last one)
		var i = all_instantiators[all_instantiators.length - 1];
		var instance = i.run();
		if (!err.ok()) return ErrorBuilder.new()
			.render(err.error_code, whamm.filename, err.section, err.error_index, err.error_pos, err.error_msg)
			.exit(null);
		var wi = WhammInstance.new(module, instance);
		whammInstances = List.new(wi, whammInstances);
		return wi;
	}

	def onFinish(i: Instance, r: Result) {
		def wi = getWhammInstance(i.module);
		if (wi == null) return;
		def exports = wi.instance.exports;
		// TODO make wasm:exit part of parsing whamm module
		for (i < exports.length) {
			var export_name = wi.instance.module.exports[i].0;
			if (Strings.equal("wasm:exit", export_name)) {
				var lookupR = wi.instance.exports[i];
				if (Function.?(lookupR)) return invokeWasmExit(wi, Function.!(lookupR));
			}
		}
	}
	def invokeWasmExit(wi: WhammInstance, func: Function) {
		// Record the time taken executing the reporting logic.
		if (Trace.whamm) Trace.OUT.puts("wasm:exit call!").ln();
		var before_us = Metrics.whamm_report_time_us.ticks();
		def result = Execute.call(func, Values.NONE); // XXX: runs on a new stack
		match (result) {
			Throw(thrown) => {
				if (Trace.whamm) Trace.OUT.puts("wasm:exit threw an error").ln();
				TraceUtil.renderThrowable(Trace.OUT, thrown);
			}
			_ => {} // ignore
		}
		Metrics.whamm_report_time_us.updateDiff(before_us);
	}
}
