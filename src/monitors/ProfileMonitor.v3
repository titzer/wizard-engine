// Copyright 2023 Wizard Authors. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

// A general-purpose profiler.
def monitor_ = MonitorRegistry.add(
	"profile", "Reports execution time for entire calling context trees.",
	ProfileMonitor.new());
def depthOpt = monitor_.options.newIntOption("depth", -1, "limit the maximum call tree depth");
def dotGraphOpt = monitor_.options.newFlag("dot", null, "print the call graph (.dot) instead");
def filterOpt = monitor_.options.newDeclFilterOption("calls", "apply profiling to selected functions");

// variables & functions for dot graph functionality
var callStack: Vector<(string, int)> = Vector<(string, int)>.new(); // (name, ID)
var ID: int;

class ProfileMonitor extends Monitor {
	var tree: ProfileTree;
	var maxDepth: int = -1;
	var filter: DeclFilter;

	// format: --monitors=profile{depth=2,calls=*}
	def configure(args: string) -> string {
		maxDepth = depthOpt.val;
		filter = filterOpt.val;
		return null;
	}
	def onParse(module: Module, err: ErrorGen) {
		var mm = ModuleInstrumenter.new(module);
		tree = ProfileTree.new();
		mm.forEachFuncFiltered(filter, ModuleInstrumenter.beforeFuncExecAndReturn(_, _, tree.enterFunc, tree.exitFunc));
	}
	def onFinish(i: Instance, r: Result) {
		if (dotGraphOpt.val) 
			tree.renderDot(out, maxDepth);
		else
			tree.rootNode.render(out, 0, 0, maxDepth);
	}
}

private class ProfileTreeNode(parent: ProfileTreeNode, func: WasmFunction) {
	var children: List<ProfileTreeNode>;
	var totalTime: int;
	var curTime: int;
	var count: int;
	def thresholds = [75, 25, 10];

	def getOrCreateChild(func: WasmFunction) -> ProfileTreeNode {
		for (i = children; i != null; i = i.tail) {
			if (i.head.func == func) return i.head;
		}
		var childNode = ProfileTreeNode.new(this, func);
		children = List.new(childNode, children);
		return childNode;
	}
	def enter() {
		curTime = System.ticksUs();
		count++;
	}
	def exit() {
		totalTime += System.ticksUs() - curTime;
	}

	def render(out: TraceBuilder, depth: int, total: int, maxDepth: int) {
		var totalPercentTime = if(depth == 1, totalTime, total);
		var nestedTime: int = totalTime;
		var selfTime: int = totalTime;

		var childArray = Lists.toArray(children);
		childArray = Arrays.sort(childArray, 0, childArray.length, byTime);

		if (maxDepth == -1 || depth < maxDepth) {
			var childTotalTime = 0;
			for (i < childArray.length) {
				childTotalTime += childArray[i].totalTime;
			}
			selfTime = nestedTime - childTotalTime;
		} else {
			selfTime = -1;
		}

		if (func == null) {
			// Root node in ProfileTree
			renderHeader(out);
		} else {
			var l = out.length;
			var f = func.decl;
			for (i < depth) out.puts("  ");
			out.beginColor(Color.FUNC);
			out.puts("func ");
			f.render(func.instance.module.names, out);
			out.endColors();
			out.pad(' ', l + 50);
			out.rjustify(' ', TraceUtil.renderUs(out, nestedTime), 12);
			out.rjustify(' ', TraceUtil.renderUs(out, selfTime), 12);
			if (totalPercentTime == 0) {
				out.beginColor(Color.UNREACHABLE);
				out.rjustify(' ', TraceUtil.renderPercent(out, 0, 1, 3), 10);
			}
			else {
				var percenti = 100 * selfTime / totalPercentTime;
				if (percenti > thresholds[0]) out.beginColor(Palettes.spectrum[0]);
				else if (percenti > thresholds[1]) out.beginColor(Palettes.spectrum[1]);
				else if (percenti > thresholds[2]) out.beginColor(Palettes.spectrum[2]);
				out.rjustify(' ', TraceUtil.renderPercent(out, selfTime, totalPercentTime, 3), 10);
			}
			out.rjustify(' ', TraceUtil.renderedLength(out, TraceBuilder.putd(_, count)), 10);
			out.endColors();
			out.ln();
		}

		if (maxDepth == -1 || depth < maxDepth) {
			for (child in childArray) {
				if (child != null) child.render(out, depth + 1, totalPercentTime, maxDepth);
			}
		}
	}
	private def byTime(a: ProfileTreeNode, b: ProfileTreeNode) -> bool {
		return b.totalTime < a.totalTime;
	}

	private def renderHeader(out: TraceBuilder) {
		var l = out.length;
		var padding = 50;
		if (Palettes.get().on) padding -= TermColors.WHITE.length;
		out.beginColor(Color.UNDERLINE);
		out.puts("  *");
		out.pad(' ', l + padding);
		out.rjustify(' ', TraceUtil.renderedLength(out, TraceBuilder.puts(_, "nested")), 12);
		out.rjustify(' ', TraceUtil.renderedLength(out, TraceBuilder.puts(_, "self")), 12);
		out.rjustify(' ', TraceUtil.renderedLength(out, TraceBuilder.puts(_, "self %")), 10);
		out.rjustify(' ', TraceUtil.renderedLength(out, TraceBuilder.puts(_, "count")), 10);
		out.endColors();
		out.ln();
	}

	def renderGraph(out: TraceBuilder, depth: int, maxDepth: int, overall: int) {
		overall = if(depth == 1, totalTime, overall);
		var selfTime: int = totalTime;
		var childArray = Lists.toArray(children);

		if (maxDepth == -1 || depth < maxDepth) {
			var childTotalTime = 0;
			for (i < childArray.length) childTotalTime += childArray[i].totalTime;
			selfTime = totalTime - childTotalTime;
		}
		var percenti = if (overall == 0, 0 , 100 * selfTime / overall);
		var fontcolor = if (percenti > thresholds[1], "red", if (percenti > thresholds[2], "darkorange", "black"));

		if (func != null) {
			var name: string = getFuncName(func);
			out.put3("  \"%s_%d\" [label=\"%s\\n", name, ++ID, name);
			if (overall == 0) TraceUtil.renderPercent(out, 0, 1, 3);
			else TraceUtil.renderPercent(out, selfTime, overall, 3);
			out.put1("\" fontcolor=%s];", fontcolor);
			out.ln();
			if (depth >= 2) { // parent != null && parent.func != null
				out.put2("  \"%s_%d\" -> ", callStack[depth-2].0, callStack[depth-2].1);
				out.put2("\"%s_%d\";", name, ID);
				out.ln();
			}

			if (depth-1 < callStack.length)
				callStack[depth-1] = (name, ID);
			else if (depth == callStack.length + 1) {
				callStack.put((name, ID));
			} else {
				out.puts("error!");
				out.ln();
				return;
			}
		}

		if (maxDepth == -1 || depth < maxDepth) 
			for (child in childArray) 
				if (child != null) child.renderGraph(out, depth + 1, maxDepth, overall);
	}
}
def getFuncName(f: WasmFunction) -> string {
	var names = f.instance.module.names;
	var name = if (names != null, names.getFuncName(f.decl.func_index));
	if (name != null) { return name; }
	else {
		var b = StringBuilder.new();
		b.put1("#%d", f.decl.func_index);
		return b.toString();
	}
}

private class ProfileTree {
	var rootNode: ProfileTreeNode;
	var curNode: ProfileTreeNode;

	new() {
		rootNode = ProfileTreeNode.new(null, null);
		curNode = rootNode;
	}
	def enterFunc(dynamicLoc: DynamicLoc) -> ProbeAction {
		curNode = curNode.getOrCreateChild(dynamicLoc.func);
		curNode.enter();
		return ProbeAction.Continue;
	}
	def exitFunc(dynamicLoc: DynamicLoc) -> ProbeAction {
		pop();
		return ProbeAction.Continue;
	}
	def pop() {
		curNode.exit();
		curNode = curNode.parent;
	}
	def renderDot(out: TraceBuilder, maxDepth: int) {
		callStack.clear();
		ID = 0;
		out.puts("digraph G {"); 
		out.ln();
		rootNode.renderGraph(out, 0, maxDepth, -1);
		out.puts("}"); 
		out.ln();
	}
}
