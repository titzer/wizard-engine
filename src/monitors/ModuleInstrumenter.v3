// Copyright 2023 Wizard Authors. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

// The ModuleInstrumenter interface implements an API for inserting callbacks for various events that
// occur during execution. Events are detected and instrumented using the lower-level probe API
// offered by the engine.
class ModuleInstrumenter(module: Module) {
	// Attach callback {f} to be called before {func} is executed.
	def beforeFuncExec(func: FuncDecl, f: DynamicLoc -> ProbeAction) -> Probe {
		if (func.imp != null) return null;
		return beforeFuncExecAndReturn(func, f, null).0;
	}
	// Attach callback {f} to be called before {func} returns.
	def beforeFuncReturn(func: FuncDecl, f: DynamicLoc -> ProbeAction) -> Probe {
		if (func.imp != null) return null;
		return beforeFuncExecAndReturn(func, null, f).1;
	}
	// Attach callbacks that are called when a func is executed or returns
	def beforeFuncExecAndReturn(func: FuncDecl, beforeExec: DynamicLoc -> ProbeAction, beforeRet: DynamicLoc -> ProbeAction) -> (Probe, Probe) {
		var entryProbe: Probe;
		var exitProbe: Probe;
		if(beforeExec != null) {
			var entryProbe = CallbackProbe.new(beforeExec);		
			Instrumentation.insertLocalProbe(module, func.func_index, 0, entryProbe);
		} 
		if(beforeRet != null) {
			var exitProbe = CallbackProbe.new(beforeRet);
			for (bi = BytecodeIterator.new().reset(func); bi.more(); bi.next()) {
				var op = bi.current();
				match (op) {
					RETURN => ; // instrument all returns
					END => if (bi.pc + 1 < bi.func.cur_bytecode.length) continue; // instrument last END
					// TODO: instrument RETURN_CALL, THROW, RETHROW
					_ => continue;
				}
				Instrumentation.insertLocalProbe(module, func.func_index, bi.pc, exitProbe);
			}
		}
		return (entryProbe, exitProbe);
	}
	// Attach callback {f} to any non-import function execution.
	def beforeAllFuncExec(f: DynamicLoc -> ProbeAction) {
		forEachFunc(ModuleInstrumenter.beforeFuncExec(_, _, f));
	}
	// Attach callback {f} to any non-import function return.
	def beforeAllFuncReturn(f: DynamicLoc -> ProbeAction) {
		forEachFunc(ModuleInstrumenter.beforeFuncReturn(_, _, f));
	}
	// Attach callback {f} to any non-import function call or return.
	def beforeAllFuncExecAndReturn(callFn: DynamicLoc -> ProbeAction, retFn: DynamicLoc -> ProbeAction) {
		forEachFunc(ModuleInstrumenter.beforeFuncExecAndReturn(_, _, callFn, retFn));
	}
	// Attach callback {f} to before an instruction execution.
	def beforeInstrExec(func: FuncDecl, pc: int, f: DynamicLoc -> ProbeAction) -> Probe {
		var probe = CallbackProbe.new(f);
		Instrumentation.insertLocalProbe(module, func.func_index, pc, probe);
		return probe;
	}
	// Attach callback {f} that fires after an instruction has executed
	// The resultant DynamicLoc will be the next wasm bytecode that is executed,
	// skipping the entry/exit of any host functions in between.
	def afterInstrExec(func: FuncDecl, pc: int, f: DynamicLoc -> ProbeAction) {
		var bi = BytecodeIterator.new();
		var it = bi.reset(func);
		var visitor = AfterInstrVisitor.new(module, func, pc, f);
		visitor.bi = it;

		it.current();
		while (it.pc < pc) {
			it.dispatch(visitor);
			it.next();
		}
		it.dispatch(visitor);
	}
	// Attach callback {f} to before a read of the global at index {global_index}.
	def beforeGlobalRead(f: DynamicLoc -> ProbeAction, global_index: u31) {
		forEachFunc(ModuleInstrumenter.instrumentGlobalAccess(_, _, global_index, false, f));
	}
	// Attach callback {f} to before a write of the global at index {global_index}.
	def beforeGlobalWrite(f: DynamicLoc -> ProbeAction, global_index: u31) {
		forEachFunc(ModuleInstrumenter.instrumentGlobalAccess(_, _, global_index, true, f));
	}
	// Attach callback {f} to before every memory read.
	def beforeMemRead(f: (DynamicLoc, Memory, u64, u64) -> ProbeAction) {
		beforeMemOp(null, f, null, null);
	}
	// Attach callback {f} to before every memory write.
	def beforeMemWrite(f: (DynamicLoc, Memory, u64, u64) -> ProbeAction) {
		beforeMemOp(null, null, f, null);
	}
	// Attach callback {f} to before every memory grow.
	def beforeMemGrow(f: (DynamicLoc, Memory, u32) -> ProbeAction) {
		beforeMemOp(null, null, null, f);
	}
	// Attach callback {f} to before every memory read for matched functions.
	def beforeMemReadMatching(matcher: (Module, FuncDecl) -> bool, f: (DynamicLoc, Memory, u64, u64) -> ProbeAction) {
		beforeMemOp(matcher, f, null, null);
	}
	// Attach callback {f} to before every memory write for matched functions.
	def beforeMemWriteMatching(matcher: (Module, FuncDecl) -> bool, f: (DynamicLoc, Memory, u64, u64) -> ProbeAction) {
		beforeMemOp(matcher, null, f, null);
	}
	// Attach callback {f} to before every memory grow for matched functions.
	def beforeMemGrowMatching(matcher: (Module, FuncDecl) -> bool, f: (DynamicLoc, Memory, u32) -> ProbeAction) {
		beforeMemOp(matcher, null, null, f);
	}

	// Helpers
	// call f(funcDecl)
	def forEachFunc<T>(f: (ModuleInstrumenter, FuncDecl) -> T) {
		for (i < module.functions.length) {
			var func = module.functions[i];
			if (func.imp != null) continue;
			f(this, func);
		}
	}
	// call f(funcDecl) if filter(module, funcDecl) is true
	def forEachFuncMatching<T>(filter: (Module, FuncDecl) -> bool, f: (ModuleInstrumenter, FuncDecl) -> T) {
		for (i < module.functions.length) {
			var func = module.functions[i];
			if (func.imp != null) continue;
			if (!filter(module, func)) continue;
			f(this, func);
		}
	}
	// call f(funcDecl) if filter(module, funcDecl) is true
	def forEachFuncFiltered<T>(filter: DeclFilter, f: (ModuleInstrumenter, FuncDecl) -> T) {
		if (filter == null) return forEachFunc(f);
		for (i < module.functions.length) {
			var func = module.functions[i];
			if (func.imp != null) continue;
			if (filter != null && !filter.matches(module, func)) continue;
			f(this, func);
		}
	}

	private def instrumentGlobalAccess(func: FuncDecl, global_index: u31, isWrite: bool, f: DynamicLoc -> ProbeAction) {
		var bi = BytecodeIterator.new();
		var visitor = GlobalVisitor.new(module, f, isWrite, global_index);
		visitor.bi = bi;

		for (it = bi.reset(func); it.more(); it.next()) {
			it.dispatch(visitor);
		}
	}
	private def beforeMemOp(
			matcher: (Module, FuncDecl) -> bool,
			readFn: (DynamicLoc, Memory, u64, u64) -> ProbeAction,
			writeFn: (DynamicLoc, Memory, u64, u64) -> ProbeAction,
			growFn: (DynamicLoc, Memory, u32) -> ProbeAction
			) {
		var it = BytecodeIterator.new();
		var visitor = MemoryVisitor.new(module, readFn, writeFn, growFn);
		visitor.bi = it;
		if (matcher != null)
			forEachFuncMatching(matcher, ModuleInstrumenter.instrumentMemoryAccesses(_, _, visitor));
		else
			forEachFunc(ModuleInstrumenter.instrumentMemoryAccesses(_, _, visitor));
	}
	private def instrumentMemoryAccesses(func: FuncDecl, visitor: MemoryVisitor) {
		for (bi = visitor.bi.reset(func); bi.more(); bi.next()) {
			bi.dispatch(visitor);
		}
	}
}

private class FuncWithLoopEnterProbe(frameAccessors: ListStack<FrameAccessor>, f: DynamicLoc -> ProbeAction) extends Probe {
	def fire(dynamicLoc: DynamicLoc) -> ProbeAction {
		var curAccessor = dynamicLoc.frame.getFrameAccessor();
		if (!frameAccessors.empty() && curAccessor == frameAccessors.peek())
			return ProbeAction.Continue;
		frameAccessors.push(curAccessor);
		if (f != null)
			return f(dynamicLoc);
		return ProbeAction.Continue;
	}
}
private class FuncWithLoopExitProbe(frameAccessors: ListStack<FrameAccessor>, f: DynamicLoc -> ProbeAction) extends Probe {
	def fire(dynamicLoc: DynamicLoc) -> ProbeAction {
		frameAccessors.pop();
		if (f != null) return f(dynamicLoc);
		return ProbeAction.Continue;
	}
}

private class AfterInstrGlobalProbe(f: DynamicLoc -> ProbeAction) extends Probe {
	def fire(dynamicLoc: DynamicLoc) -> ProbeAction {
		Instrumentation.removeGlobalProbe(this);
		return f(dynamicLoc);
	}
}
// This probe dynamically activates probes at destinations, based on the current
// state of the program. This is attached for after instruction callbacks that
// have multiple destinations possible at runtime, with the exception of
// CALL_INDIRECT and RETURN/END of function, where it activates the global
// interpreter probe instead.
private class AfterInstrProbe(module: Module, opcode: Opcode, destProbes: Range<AfterInstrDestProbe>, f: DynamicLoc -> ProbeAction) extends Probe {
	def fire(dynamicLoc: DynamicLoc) -> ProbeAction {
		var nextProbe = getNextProbe(dynamicLoc);
		if (nextProbe != null) {
			nextProbe.active = true;
		}
		return ProbeAction.Continue;
	}

	// Get the next destination probe based on the state of the current frame
	private def getNextProbe(dynamicLoc: DynamicLoc) -> AfterInstrDestProbe {
		if (destProbes.length == 1) return destProbes[0];

		match (opcode) {
			BR_IF, BR_TABLE, IF => ;
			_ => return null;
		}

		var accessor = dynamicLoc.frame.getFrameAccessor();
		var condition = Values.unbox_i(accessor.getOperand(0));

		match (opcode) {
			BR_IF => return destProbes[if(condition != 0, 1, 0)];
			BR_TABLE => {
				if (condition >= destProbes.length) condition = destProbes.length - 1;
				return destProbes[condition];
			}
			IF => return destProbes[if(condition != 0, 1, 0)];
			_ => ;
		}
		return null;
	}
}
private class AfterInstrAddGlobalProbe(f: DynamicLoc -> ProbeAction) extends Probe {
	def fire(dynamicLoc: DynamicLoc) -> ProbeAction {
		Instrumentation.insertGlobalProbe(AfterInstrGlobalProbe.new(f));
		return ProbeAction.Continue;
	}
}
private class AfterInstrDestProbe(module: Module, f: DynamicLoc -> ProbeAction) extends Probe {
	var active = false;

	def fire(dynamicLoc: DynamicLoc) -> ProbeAction {
		if (active) {
			active = false;
			return f(dynamicLoc);
		}
		return ProbeAction.Continue;
	}
}
private class AfterInstrVisitor(module: Module, func: FuncDecl, pc: int, f: DynamicLoc -> ProbeAction) extends BytecodeVisitor {
	var bi: BytecodeIterator;
	var stp: int;
	var sidetable = func.sidetable.entries; // TODO: use Sidetable abstraction

	def visitOp(op: Opcode) {
		insertProbes([bi.nextPc()], f);
	}
	def visit_BR(depth: u31) {
		var takenPc = sidetable[stp] + bi.pc;
		stp += 4;
		insertProbes([takenPc], f);
	}
	def visit_BR_IF(depth: u31) {
		var notTakenPc = bi.nextPc();
		var takenPc = sidetable[stp] + bi.pc;
		stp += 4;
		insertProbes([notTakenPc, takenPc], f);
	}
	def visit_BR_TABLE(labels: Range<u31>) {
		stp += 4;
		var dests = Array<int>.new(labels.length);
		for (i < labels.length) {
			dests[i] = sidetable[stp] + bi.pc;
			stp += 4;
		}
		insertProbes(dests, f);
	}
	def visit_IF(btc: BlockTypeCode) {
		var elsePc = sidetable[stp] + bi.pc;
		stp += 4;
		insertProbes([elsePc, bi.nextPc()], f);
	}
	def visit_ELSE() {
		var takenPc = sidetable[stp] + bi.pc;
		stp += 4;
		insertProbes([takenPc], f);
	}
	def visit_END() {
		if ((bi.pc + 1) < bi.func.cur_bytecode.length) {
			insertProbes([bi.nextPc()], f);
		} else {
			insertGlobalProbe(f);
		}
	}
	def visit_CALL(func_index: u31) {
		insertCallProbe(func_index, f);
	}
	def visit_RETURN() {
		insertGlobalProbe(f);
	}
	def visit_CALL_INDIRECT(sig_index: u31, table_index: u31) {
		insertGlobalProbe(f);
	}

	private def insertProbes(dests: Range<int>, f: DynamicLoc -> ProbeAction) {
		if (bi.pc != pc) return;
		var destProbes = Array<AfterInstrDestProbe>.new(dests.length);
		for (i < dests.length) {
			if (dests[i] == -1) {
				destProbes[i] = null;
			} else {
				destProbes[i] = AfterInstrDestProbe.new(module, f);
				Instrumentation.insertLocalProbe(module, bi.func.func_index, dests[i], destProbes[i]);
			}
		}
		Instrumentation.insertLocalProbe(module, bi.func.func_index, bi.pc, AfterInstrProbe.new(module, bi.current(), destProbes, f));
	}
	private def insertCallProbe(func_index: u31, f: DynamicLoc -> ProbeAction) {
		if (bi.pc != pc) return;
		var destProbe = AfterInstrDestProbe.new(module, f);
		var func_it = BytecodeIterator.new();
		var func_bi = func_it.reset(module.functions[func_index]);
		func_bi.current();
		Instrumentation.insertLocalProbe(module, func_index, func_bi.pc, destProbe);
		Instrumentation.insertLocalProbe(module, bi.func.func_index, bi.pc, AfterInstrProbe.new(module, bi.current(), [destProbe], f));
	}
	private def insertGlobalProbe(f: DynamicLoc -> ProbeAction) {
		if (bi.pc != pc) return;
		Instrumentation.insertLocalProbe(module, bi.func.func_index, bi.pc, AfterInstrAddGlobalProbe.new(f));
	}
}

private class GlobalVisitor(module: Module, f: DynamicLoc -> ProbeAction, isWrite: bool, targetIndex: u31) extends BytecodeVisitor {
	var bi: BytecodeIterator;

	def visit_GLOBAL_GET(global_index: u31) {
		if (global_index == targetIndex && !isWrite) {
			Instrumentation.insertLocalProbe(module, bi.func.func_index, bi.pc, CallbackProbe.new(f));
		}
	}

	def visit_GLOBAL_SET(global_index: u31) {
		if (global_index == targetIndex && isWrite) {
			Instrumentation.insertLocalProbe(module, bi.func.func_index, bi.pc, CallbackProbe.new(f));
		}
	}
}

class MemoryVisitor(
		module: Module,
		loadFn: (DynamicLoc, Memory, u64, u64) -> ProbeAction,
		storeFn: (DynamicLoc, Memory, u64, u64) -> ProbeAction,
		growFn: (DynamicLoc, Memory, u32) -> ProbeAction
		) extends BytecodeVisitor {
	var bi: BytecodeIterator;

	def visitLoad(op: Opcode, imm: MemArg, size: u8) {
		// TODO: Take into account lane_index for V128 operations
		if (loadFn == null) return;
		Instrumentation.insertLocalProbe(module, bi.func.func_index, bi.pc, MemoryAccessProbe.new(imm, 0, size, loadFn));
	}
	def visitStore(op: Opcode, imm: MemArg, size: u8) {
		if (storeFn == null) return;
		Instrumentation.insertLocalProbe(module, bi.func.func_index, bi.pc, MemoryAccessProbe.new(imm, -1, size, storeFn));
	}

	def visit_MEMORY_GROW(memory_index: u31) {
		if (growFn == null) return;
		Instrumentation.insertLocalProbe(module, bi.func.func_index, bi.pc, MemoryGrowProbe.new(memory_index, growFn));
	}
}
private class MemoryAccessProbe(imm: MemArg, operand: int, size: u64, f: (DynamicLoc, Memory, u64, u64) -> ProbeAction) extends Probe {
	def fire(dynamicLoc: DynamicLoc) -> ProbeAction {
		var accessor = dynamicLoc.frame.getFrameAccessor();
		var address: u64 = Values.unbox_u(accessor.getOperand(operand));
		address += imm.offset;

		var memory = dynamicLoc.func.instance.memories[imm.memory_index];

		return f(dynamicLoc, memory, address, size);
	}
}
private class MemoryGrowProbe(memory_index: u31, f: (DynamicLoc, Memory, u32) -> ProbeAction) extends Probe {
	def fire(dynamicLoc: DynamicLoc) -> ProbeAction {
		var accessor = dynamicLoc.frame.getFrameAccessor();
		var pages = Values.unbox_u(accessor.getOperand(0));
		var memory = dynamicLoc.func.instance.memories[memory_index];
		return f(dynamicLoc, memory, pages);
	}
}
