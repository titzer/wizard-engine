// Copyright 2020 Ben L. Titzer. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

// Main entrypoint to an wasm file format dumping utility.
def main(args: Array<string>) -> int {
	var engine = Engine.new();
	var err = ErrorGen.new("objdump");

	engine.extensions = Extension.set.all;

	// Parse options and extract module and module arguments
	for (i < args.length) {
		var a = args[i];
		if (a.length > 0 && a[0] == '-') {
			err.abs(u64.!(i));
			if (!OptionsRegistry.parse(a, err)) return ErrorBuilder.new()
				.puts("objdump: unrecognized option ")
				.putsq(a)
				.ln()
				.exit(1);
			args[i] = null;
			engine.extensions = EngineOptions.extensions;
		} else if (!Strings.endsWith(a, ".wasm")) {
			return ErrorBuilder.new()
				.puts(a)
				.puts(": unknown file format, expected .wasm\n")
				.exit(4);
		}
	}

	for (a in args) {
		if (a == null) continue;
		var exit = parseAndDump(engine, a);
		if (exit != 0) return exit;
	}
	return 0;
}

def parseAndDump(engine: Engine, path: string) -> int {
	// Parse the binary module
	var err = ErrorGen.new(path);
	var module = engine.loadWasmFile(path, err, null);
	if (err.error()) return ErrorBuilder.new()
		.render(err.error_code, path, err.section, err.error_index, err.error_pos, err.error_msg)
		.exit(3);

	var names = if(module.names != null, module.names.lazyDecode());
	// Dump the binary module
	var OUT = Trace.OUT;
	for (i < module.heaptypes.length) {
		var h = module.heaptypes[i];
		OUT.put2("types[%d] = %q ", i, h.render);
		renderSuperTypes(OUT, h.supertypes, HeapType.render);
		OUT.ln();
	}
	for (i < module.imports.length) {
		var d = module.imports[i];
		OUT.put1("imports[%d] = ", i);
		OUT.putsq(d.imp.module_name);
		OUT.putc('.');
		OUT.putsq(d.imp.field_name);
		renderImportExportDecl(OUT, d);
		OUT.ln();
	}
	for (i < module.functions.length) {
		var f = module.functions[i];
		OUT.put2("functions[%d] = %q", i, f.render(names, _));
		OUT.put1(": %q", f.sig.render);
		if (f.imp == null) OUT.put1(" { %d bytes }", f.orig_bytecode.length);
		OUT.ln();
	}
	for (i < module.tables.length) {
		var t = module.tables[i];
		OUT.put1("tables[%d] default elem = ", i);
		match (t.default_elem) {
			I32 => OUT.puts("I32");
			I64 => OUT.puts("I64");
			F32 => OUT.puts("F32");
			F64 => OUT.puts("F64");
			V128 => OUT.puts("V128");
			FuncRefNull => OUT.puts("FuncRefNull");
			RefNull => OUT.puts("RefNull");
			ExternRefNull => OUT.puts("ExternRefNull");
			Global => OUT.puts("Global");
			FuncRef => OUT.puts("FuncRef");
//			ContRef => OUT.puts("ContRef");
			Const => OUT.puts("Const");
			I31 => OUT.puts("I31");
			Array => OUT.puts("Array");
			FixedArray => OUT.puts("FixedArray");
			Struct => OUT.puts("Struct");
			ArrayNewData => OUT.puts("ArrayNewData");
			ArrayNewElem => OUT.puts("ArrayNewElem");
			I32_ADD(a, b) => OUT.puts("i32.add");
			I32_SUB(a, b) => OUT.puts("i32.sub");
			I32_MUL(a, b) => OUT.puts("i32.mul");
			I64_ADD(a, b) => OUT.puts("i64.add");
			I64_SUB(a, b) => OUT.puts("i64.sub");
			I64_MUL(a, b) => OUT.puts("i64.mul");
		}
		OUT.ln();
	}
	for (i < module.tags.length) {
		var t = module.tags[i];
		OUT.put1("tags[%d] = ", i);
		if (t.fields != null) {
			for (value in t.fields) {
				OUT.put1("value: %q", value.render);
				OUT.ln();
			}
		}
		OUT.ln();
	}
	for (i < module.exports.length) {
		var e = module.exports[i];
		OUT.put1("exports[%d] = ", i);
		OUT.putsq(e.0);
		renderImportExportDecl(OUT, e.1);
		OUT.ln();
	}
	OUT.put1("new functions: %d", module.new_funcs);
	OUT.ln();
	return 0;
}
def renderImportExportDecl(out: StringBuilder, decl: Decl) {
	match (decl) {
		x: SigDecl => out.put1(": sig #%d", x.heaptype_index);
		x: StructDecl => out.put1(": struct #%d", x.heaptype_index);
		x: ArrayDecl => out.put1(": array #%d", x.heaptype_index);
		x: FuncDecl => out.put2(": func #%d: %q", x.func_index, x.sig.render);
		x: TableDecl => out.put1(": table #%d", x.table_index);
		x: MemoryDecl => out.put1(": memory #%d", x.memory_index);
		x: GlobalDecl => out.put1(": global #%d", x.global_index);
		x: TagDecl => out.put1(": tag #%d", x.tag_index);
	}
}
def renderSuperTypes<T>(out: StringBuilder, t: Array<T>, render: (T, StringBuilder) -> StringBuilder) -> StringBuilder {
	if (t.length == 0) return out;
	out.puts(" <: ");
	return Trace.renderCspRange(out, t, render);
}
