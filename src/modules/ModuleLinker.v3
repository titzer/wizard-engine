// Copyright 2023 Wizard Authors. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

// Links the main module with the pre-loaded modules and returns the main module instance.
// The linker partially instantiates all the modules, then resolve each import to its original
// provider, then completes the instantiation. This allows bi-directional imports among multiple
// modules, as long as the import chain is acyclic.
class ModuleLinker(engine: Engine,
		   program_args: Array<string>,
		   error: ErrorGen) {
	def named_modules = Vector<NamedModule>.new();
	def lookup = Vector<string -> ImportProcessor>.new();
	var binder: (Decl, Exportable) -> Exportable;
	private def instantiators = Strings.newMap<(Instantiator, int)>();
	private var exportable_cache: Array<Array<(Exportable, Decl)>>;
	private var deferred_imports: Array<Array<int>>;

	def addImportProcessor(p: ImportProcessor) {
		if (p != null) lookup.put(singleLookup(p.name, p, _));
	}
	def link() -> Array<Instantiator> {
		exportable_cache = Array.new(named_modules.length);
		deferred_imports = Array.new(named_modules.length);

		if (Trace.linking) Trace.OUT.puts("linking: ");
		for (i < named_modules.length) {
			var nm = named_modules[i];
			if (Trace.linking) {
				if (i > 0) Trace.OUT.csp();
				Trace.OUT.puts(nm.name);
			}
			var instantiator = partiallyInstantiate(i);
			if (error.error()) return null;
			instantiators[nm.name] = (instantiator, i);
			if (nm.module.exports.length > 0) {
				exportable_cache[i] = Array<(Exportable, Decl)>.new(nm.module.exports.length);
			}
		}
		if (Trace.linking) Trace.OUT.ln();
		for (i < named_modules.length) {
			if (deferred_imports[i].length == 0) continue;
			var nm = named_modules[i];
			var instantiator = instantiators[nm.name].0;
			var imports = instantiator.imports;
			for (j < nm.module.imports.length) {
				if (instantiator.imports[j] == null) {
					var imp_decl = nm.module.imports[j];
					var requested_mod = imp_decl.imp.module_name;
					var requested_field = imp_decl.imp.field_name;
					instantiator.imports[j] = resolveImport(
							imp_decl,
							i,
							requested_mod,
							requested_field,
							named_modules.length - 1).0;
					if (error.error()) return null;
				}
			}
			instantiator.runPartial(deferred_imports[i]);
			if (error.error()) return null;
		}
		var all_instantiators = Array<Instantiator>.new(named_modules.length);
		for (i < named_modules.length) all_instantiators[i] = instantiators[named_modules[i].name].0;
		return all_instantiators;
	}
	private def partiallyInstantiate(module_id: int) -> Instantiator {
		var name = named_modules[module_id].name;
		var module = named_modules[module_id].module;
		var imports = Array<Exportable>.new(module.imports.length);
		var i = Instantiator.new(engine.extensions, module, imports, error);
		i.binder = binder;
		var deferred_imports = Vector<int>.new();
		var processor_map = Strings.newMap<ImportProcessor>();
		for (j < module.imports.length) {
			var imp_mod_name = module.imports[j].imp.module_name;
			// first try to match the import to loaded wasm modules,
			// then instantiate host module processors
			if (isDynamicImport(imp_mod_name, name)) {
				deferred_imports.put(j);
				continue;
			}
			if (processor_map[imp_mod_name] != null) continue;
			var processor = resolveImportProcessor(imp_mod_name);
			if (processor == null) {
				error.index = module_id;
				error.abs(u64.view(j)).setc(WasmError.IMPORT_COUNT_MISMATCH, StringBuilder.new()
					.put2("%s.wasm: import from unknown module \"%s\"\n", name, imp_mod_name).toString());
				return null;
			}
			processor_map[imp_mod_name] = processor;
			i.processors.put(processor);
		}
		this.deferred_imports[module_id] = deferred_imports.extract();
		i.runPartial(this.deferred_imports[module_id]);
		return i;
	}
	// finds the Exportable instance for each import from a partially instantiated wasm module.
	private def resolveImport(
			imp_decl: Decl,
			importer_id: int,
			requested_module: string,
			field_name: string,
			max_steps: int) -> (Exportable, Decl) {
		var requested_ins = instantiators[requested_module].0.instance;
		var req_mod_id = instantiators[requested_module].1;
		var importer_mod_name = named_modules[importer_id].name;
		error.index = importer_id;
		for (exp_id < requested_ins.module.exports.length) {
			var exp = requested_ins.module.exports[exp_id];
			if (Strings.equal(exp.0, field_name)) {
				// check cache first
				var exportable: (Exportable, Decl);
				if (exportable_cache[req_mod_id][exp_id].0 != null) {
					exportable = exportable_cache[req_mod_id][exp_id];
				} else {
					if (requested_ins.exports[exp_id] != null) {
						exportable = (requested_ins.exports[exp_id], exp.1);
					} else if (max_steps <= 0) {
						error.abs(u64.view(exp_id)).setc(WasmError.IMPORT_CYCLE_DETECTED, StringBuilder.new()
							.put3("%s.wasm: import cycle detected for %s.%s\n",
									importer_mod_name, requested_module, field_name)
							.toString());
						return (null, null);
					} else {
						var new_mod_name = exp.1.imp.module_name;
						var new_field_name = exp.1.imp.field_name;
						exportable = resolveImport(exp.1, req_mod_id, new_mod_name, new_field_name, max_steps - 1);
						if (error.error()) return (null, null);
					}
				}
				// cache result along the chain of "exported imports".
				exportable_cache[req_mod_id][exp_id] = exportable;
				return exportable;
			}
		}
		// TODO: factor out errors into their own methods in ErrorGen
		error.abs(u64.view(importer_id)).setc(WasmError.IMPORT_COUNT_MISMATCH, StringBuilder.new()
			.put3("%s.wasm: field \"%s\" from module \"%s\" not found\n",
				importer_mod_name, field_name, requested_module)
			.toString());
		return (null, null);
	}
	private def resolveImportProcessor(imp_mod_name: string) -> ImportProcessor {
		for (i < lookup.length) {
			var p = lookup[i](imp_mod_name);
			if (p != null) {
				var trace = BasicTracing.moduleFilter != null && BasicTracing.moduleFilter.matchesStr(imp_mod_name, null);
				if (HostModule.?(p)) HostModule.!(p).init(program_args, trace, error); // XXX: abstraction break
				return p;
			}
		}
		return null;
	}
	private def isDynamicImport(module_name: string, self_name: string) -> bool {
		if (Strings.equal(module_name, self_name)) return false;
		for (i < named_modules.length) if (Strings.equal(named_modules[i].name, module_name)) return true;
		return false;
	}
}
def singleLookup<T>(expect: string, result: T, key: string) -> T { // XXX: move to lib/util/Functions?
	if (Strings.equal(expect, key)) return result;
	var d: T;
	return d;
}
